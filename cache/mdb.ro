Cache for Windows^INT^M/DB and M/Wire Routines for Cache^~Format=Cache.S~
%RO on 20 Oct 2010  12:13 PM
MDB^INT^1^62009,84673^0^1
MDB ; M/DB: Mumps Emulation of Amazon SimpleDB
 ;
 ; ----------------------------------------------------------------------------
 ; | M/DB                                                                     |
 ; | Copyright (c) 2004-9 M/Gateway Developments Ltd,                         |
 ; | Reigate, Surrey UK.                                                      |
 ; | All rights reserved.                                                     |
 ; |                                                                          |
 ; | http://www.mgateway.com                                                  |
 ; | Email: rtweed@mgateway.com                                               |
 ; |                                                                          |
 ; | This program is free software: you can redistribute it and/or modify     |
 ; | it under the terms of the GNU Affero General Public License as           |
 ; | published by the Free Software Foundation, either version 3 of the       |
 ; | License, or (at your option) any later version.                          |
 ; |                                                                          |
 ; | This program is distributed in the hope that it will be useful,          |
 ; | but WITHOUT ANY WARRANTY; without even the implied warranty of           |
 ; | MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            |
 ; | GNU Affero General Public License for more details.                      |
 ; |                                                                          |
 ; | You should have received a copy of the GNU Affero General Public License |
 ; | along with this program.  If not, see <http://www.gnu.org/licenses/>.    |
 ; ----------------------------------------------------------------------------
 ;
 ;
version()	
	QUIT "37"
	;
buildDate()	
	QUIT "10 October 2010"
	;
indexLength()
 QUIT 180
 ;
 ; Note: keyId will have been tested and must be valid
 ; by the time these methods are called
 ;
 ; To Initialise the service: http://192.168.1.xxx/mdb/test.mgwsi?Action=Initialise
 ;
addUser(userKeyId,userSecretKey,requestId,boxUsage)
 ;
 n startTime,stop
 ;
 s requestId=$$init(.startTime)
 s ^MDBUAF("keys",userKeyId)=userSecretKey
 QUIT $$end(startTime,.boxUsage)
 ;
createDomain(keyId,domainName,requestId,boxUsage)
 ;
 n dn,dnx,id,noOfDomains,startTime,token
 ;
 s requestId=$$init(.startTime)
 s keyId=$g(keyId)
 i keyId="" QUIT $$end(startTime,.boxUsage,"MissingParameter","AWSAccessKeyId")
 s domainName=$g(domainName)
 i domainName="" QUIT $$end(startTime,.boxUsage,"MissingParameter","DomainName")
 s dn=$tr(domainName,"ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz1234567890_-.","")
 i dn'="" QUIT $$end(startTime,.boxUsage,"InvalidParameterValue",domainName,"DomainName")
 ;
 s noOfDomains=+$g(^MDB(keyId))
 i $g(^MDBConfig("DomainsPerAccount"))'="",noOfDomains=^MDBConfig("DomainsPerAccount") QUIT $$end(startTime,.boxUsage,"NumberDomainsExceeded")
 s dnx=$e(domainName,1,$$indexLength())
 i '$$domainExists(keyId,domainName) d
 . s noOfDomains=$increment(^MDB(keyId))
 . s id=$increment(^MDB(keyId,"domains"))
 . s ^MDB(keyId,"domains",id,"name")=domainName
 . s ^MDB(keyId,"domains",id,"created")=$h
 . d updateDomainMetaData(keyId,id)
 . s ^MDB(keyId,"domainIndex",dnx,id)=""
 ;
 QUIT $$end(startTime,.boxUsage)
 ;
updateDomainMetaData(keyId,id)
 ;
 s ^MDB(keyId,"domains",id,"modified")=$h
 QUIT
 ;
getDomainMetaData(keyId,domainId,metaData)
 ;
 n size,timestamp
 ;
 k metaData
 s timestamp=$g(^MDB(keyId,"domains",domainId,"modified"))
 s timestamp=$$convertToEpochTime(timestamp)
 s metaData("Timestamp")=timestamp
 s metaData("ItemCount")=$$countItems(keyId,domainId,.size)_".0"
 s metaData("ItemNamesSizeBytes")=size_".0"
 s metaData("AttributeValueCount")=$$countNVPs(keyId,domainId,.size)_".0"
 s metaData("AttributeValuesSizeBytes")=size_".0"
 s metaData("AttributeNameCount")=$$countAttributeNames(keyId,domainId,.size)_".0"
 s metaData("AttributeNamesSizeBytes")=size_".0"
 QUIT
 ;
domainMetadata(keyId,domainName,metaData,requestId,boxUsage)
 ;
 n domainId,startTime
 ;
 s requestId=$$init(.startTime)
 s keyId=$g(keyId)
 i keyId="" QUIT $$end(startTime,.boxUsage,"MissingParameter","AWSAccessKeyId")
 s domainName=$g(domainName)
 i domainName="" QUIT $$end(startTime,.boxUsage,"MissingParameter","DomainName")
 s domainId=$$getDomainId(keyId,domainName)
 i domainId="" QUIT $$end(startTime,.boxUsage,"NoSuchDomain","The specified domain does not exist.")
 ;
 d getDomainMetaData(keyId,domainId,.metaData)
 ;
 QUIT $$end(startTime,.boxUsage)
 ;
convertToEpochTime(dh)
 ;
 n time
 ;
 s time=(dh*86400)+$p(dh,",",2)
 s time=time-4070908800
 QUIT time
 ;
convertFromEpochTime(time)
 ;
 n dh
 ;
 s time=time+4070908800
 s dh=time\86400
 s time=time#86400
 QUIT dh_","_time
 ;
countItems(keyId,domainId,size)
 ;
 n count,id
 ;
 s id="",count=0,size=0
 f  s id=$o(^MDB(keyId,"domains",domainId,"items",id)) q:id=""  d
 . s count=count+1
 . s size=size+$l(^MDB(keyId,"domains",domainId,"items",id))
 QUIT count
 ;
countNVPs(keyId,domainId,size)
 ;
 n attribId,count,itemId,valueId
 ;
 s itemId="",count=0,size=0
 f  s itemId=$o(^MDB(keyId,"domains",domainId,"items",itemId)) q:itemId=""  d
 . s attribId=0
 . f  s attribId=$o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId)) q:attribId=""  d
 . . s valueId=""
 . . f  s valueId=$o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",valueId)) q:valueId=""  d
 . . . s count=count+1
 . . . s size=size+$l(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",valueId))
 QUIT count
 ;
countAttributeNames(keyId,domainId,size)
 ;
 n attribId,count,name
 ;
 s attribId=0,count=0,size=0
 f  s attribId=$o(^MDB(keyId,"domains",domainId,"attribs",attribId)) q:attribId=""  d
 . s count=count+1
 . s name=^MDB(keyId,"domains",domainId,"attribs",attribId)
 . s size=size+$l(name)
 QUIT count
 ;
domainExists(keyId,name)
 ;
 n id
 ;
 s id=$$getDomainId($g(keyId),$g(name))
 QUIT (id'="")
 ;
getDomainId(keyId,name)
 ;
 n found,id,namex
 ;
 i $g(name)="" QUIT ""
 i $g(keyId)="" QUIT ""
 s namex=$e(name,1,$$indexLength())
 s id="",found=0
 f  s id=$o(^MDB(keyId,"domainIndex",namex,id)) q:id=""  d  q:found
 . i $g(^MDB(keyId,"domains",id,"name"))=name s found=1
 i id'="",'$d(^MDB(keyId,"domains",id,"attribs",0)) d buildItemNameIndex(keyId,id)
 QUIT id
 ;
buildItemNameIndex(keyId,domainId)
 ;
 n itemId,itemValue,itemValuex
 ;
 s ^MDB(keyId,"domains",domainId,"attribs",0)="itemName()"
 s ^MDB(keyId,"domains",domainId,"attribsIndex","itemName()",0)=""
 s itemId=""
 f  s itemId=$o(^MDB(keyId,"domains",domainId,"items",itemId)) q:itemId=""  d
 . s itemValue=^MDB(keyId,"domains",domainId,"items",itemId)
 . s itemValuex=$e(itemValue,1,$$indexLength())
 . s ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",0,"value")=1
 . s ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",0,"value",1)=itemValue
 . s ^MDB(keyId,"domains",domainId,"queryIndex",0,itemValuex,itemId)=""
 QUIT 
 ;
countDomains(key)
 ;
 n id,no
 ;
 s id="",no=0
 f  s id=$o(^MDB(key,"domains",id)) q:id=""  s no=no+1
 QUIT no
 ;
deleteDomain(keyId,domainName,requestId,boxUsage)
 ;
 n dn,dnx,id,noOfDomains,startTime,token
 ;
 s requestId=$$init(.startTime)
 s keyId=$g(keyId)
 i keyId="" QUIT $$end(startTime,.boxUsage,"MissingParameter","AWSAccessKeyId")
 s domainName=$g(domainName)
 i domainName="" QUIT $$end(startTime,.boxUsage,"MissingParameter","DomainName")
 s id=$$getDomainId(keyId,domainName)
 i id="" QUIT $$end(startTime,.boxUsage)
 k ^MDB(keyId,"domains",id)
 k ^MDB(keyId,"domainIndex",$e(domainName,1,$$indexLength()),id)
 s noOfDomains=$$countDomains(keyId)
 i noOfDomains>0 d
 . s ^MDB(keyId)=noOfDomains
 e  d
 . k ^MDB(keyId)
 ;
 QUIT $$end(startTime,.boxUsage)
 ;
listDomains(keyId,maxNoOfDomains,nextToken,domainList,requestId,boxUsage)
 ;
 n domainName,fullName,id,noOfDomains,startTime,stop,token
 ;
 s requestId=$$init(.startTime)
 s keyId=$g(keyId)
 i keyId="" QUIT $$end(startTime,.boxUsage,"MissingParameter","AWSAccessKeyId")
 k domainList s noOfDomains=0,stop=0
 i $g(nextToken)="" d
 . s domainName=""
 e  d
 . s domainName=$$decodeBase64(nextToken)
 s nextToken=""
 f  s domainName=$o(^MDB(keyId,"domainIndex",domainName)) q:domainName=""  d  q:stop
 . s id=""
 . f  s id=$o(^MDB(keyId,"domainIndex",domainName,id)) q:id=""  d
 . . s fullName=$g(^MDB(keyId,"domains",id,"name"))
 . . s noOfDomains=noOfDomains+1
 . . s domainList(noOfDomains)=fullName
 . i noOfDomains=maxNoOfDomains d  q
 . . s stop=1
 . . s nextToken=$$encodeBase64(domainName)
 ;
 QUIT $$end(startTime,.boxUsage)
 ;
itemExists(keyId,domainId,name)
 ;
 n id
 ;
 s id=$$getItemId($g(keyId),$g(domainId),$g(name))
 QUIT (id'="")
 ;
getItemId(keyId,domainId,name)
 ;
 n found,id,namex
 ;
 i $g(domainId)="" QUIT ""
 i $g(name)="" QUIT ""
 i $g(keyId)="" QUIT ""
 i domainId="" QUIT ""
 s namex=$e(name,1,$$indexLength())
 s id="",found=0
 f  s id=$o(^MDB(keyId,"domains",domainId,"itemIndex",namex,id)) q:id=""  d  q:found
 . i $g(^MDB(keyId,"domains",domainId,"items",id))=name s found=1
 QUIT id
 ;
getAttributeId(keyId,domainId,name)
 ;
 n found,id,namex
 ;
 i $g(domainId)="" QUIT ""
 i $g(name)="" QUIT ""
 i $g(keyId)="" QUIT ""
 i domainId="" QUIT ""
 s namex=$e(name,1,$$indexLength())
 s id="",found=0
 f  s id=$o(^MDB(keyId,"domains",domainId,"attribsIndex",namex,id)) q:id=""  d  q:found
 . i $g(^MDB(keyId,"domains",domainId,"attribs",id))'=name q
 . s found=1
 QUIT id
 ;
getAttributeValueId(keyId,domainId,itemId,attribId,value)
 ;
 n found,id,valuex
 ;
 i $g(domainId)="" QUIT ""
 i '$d(value) QUIT ""
 i $g(keyId)="" QUIT ""
 i domainId="" QUIT ""
 i value="" s value=$c(31)
 s itemId=$g(itemId)
 i itemId="" QUIT ""
 s valuex=$e(value,1,$$indexLength())
 s id="",found=0
 f  s id=$o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"valueIndex",valuex,id)) q:id=""  d  q:found
 . i $g(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",id))'=value q
 . s found=1
 QUIT id
putAttributes(keyId,domainName,itemName,attributes,requestId,boxUsage)
 ;
 n attribId,domainId,itemId,name,namex,no,replace,startTime,value,valueId,valuex,xvalue
 ;
 s requestId=$$init(.startTime)
 s keyId=$g(keyId)
 i keyId="" QUIT $$end(startTime,.boxUsage,"MissingParameter","AWSAccessKeyId")
 s domainName=$g(domainName)
 i domainName="" QUIT $$end(startTime,.boxUsage,"MissingParameter","DomainName")
 s domainId=$$getDomainId(keyId,domainName)
 i domainId="" QUIT $$end(startTime,.boxUsage,"NoSuchDomain","The specified domain does not exist.")
 s itemName=$g(itemName)
 i itemName="" QUIT $$end(startTime,.boxUsage,"MissingParameter","ItemName")
 s itemId=$$getItemId(keyId,domainId,itemName)
 i itemId="" d
 . ; add Item to Domain if it's new
 . n itemNamex
 . s itemNamex=$e(itemName,1,$$indexLength())
 . s itemId=$increment(^MDB(keyId,"domains",domainId,"items"))
 . s ^MDB(keyId,"domains",domainId,"itemIndex",itemNamex,itemId)=""
 . s ^MDB(keyId,"domains",domainId,"items",itemId)=itemName
 . s ^MDB(keyId,"domains",domainId,"attribs",0)="itemName()"
 . s ^MDB(keyId,"domains",domainId,"attribsIndex","itemName()",0)=""
 . s ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",0,"value")=1
 . s ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",0,"value",1)=itemName
 . s ^MDB(keyId,"domains",domainId,"queryIndex",0,itemNamex,itemId)=""
 ;
 ; attributes(no,"name")=attribute name
 ; attributes(no,"value")=attribute value
 ; attributes(no,"replace")=1
 ;
 s no=""
 f  s no=$o(attributes(no)) q:no=""  d
 . s name=$g(attributes(no,"name"))
 . s value=$g(attributes(no,"value"))
 . i value="" s value=$c(31)
 . s replace=+$g(attributes(no,"replace"))
 . s namex=$e(name,1,$$indexLength())
 . s valuex=$e(value,1,$$indexLength())
 . s attribId=$$getAttributeId(keyId,domainId,name)
 . i attribId="" d
 . . ; add new attribute name to the domain
 . . s attribId=$increment(^MDB(keyId,"domains",domainId,"attribs"))
 . . s ^MDB(keyId,"domains",domainId,"attribs",attribId)=name
 . . s ^MDB(keyId,"domains",domainId,"attribsIndex",namex,attribId)=""
 . s valueId=$$getAttributeValueId(keyId,domainId,itemId,attribId,value)
 . i 'replace,valueId'="" q  ; Not allowed to have more than one attribute with the same name and value
 . i replace d
 . . ; first remove any existing values for this attribute name
 . . f  s valueId=$o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",valueId)) q:valueId=""  d
 . . . s xvalue=^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",valueId)
 . . . k ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"valueIndex",xvalue,valueId)
 . . . k ^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)
 . . k ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value")
 . ; now add the new attribute name/value pair
 . s valueId=$increment(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value"))
 . s ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",valueId)=value
 . s ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"valueIndex",valuex,valueId)=""
 . s ^MDB(keyId,"domains",domainId,"queryIndex",attribId,valuex,itemId)=""
 ;
 d updateDomainMetaData(keyId,domainId)
 QUIT $$end(startTime,.boxUsage)
 ;
getAttributes(keyId,domainName,itemName,attributes,requestId,boxUsage,suppressBoxUsage)
 ;
 n attribId,attrNo,domainId,itemId,name,startTime,value,valueId,valueNo
 ;
 s requestId=""
 i '$g(suppressBoxUsage) s requestId=$$init(.startTime)
 s keyId=$g(keyId)
 i keyId="" QUIT $$end(startTime,.boxUsage,"MissingParameter","AWSAccessKeyId")
 s domainName=$g(domainName)
 i domainName="" QUIT $$end(startTime,.boxUsage,"MissingParameter","DomainName")
 s domainId=$$getDomainId(keyId,domainName)
 i domainId="" QUIT $$end(startTime,.boxUsage,"NoSuchDomain","The specified domain does not exist.")
 s itemName=$g(itemName)
 i itemName="" QUIT $$end(startTime,.boxUsage,"MissingParameter","ItemName")
 s itemId=$$getItemId(keyId,domainId,itemName)
 i itemId="" QUIT $$end(startTime,.boxUsage,"NoSuchItemName","The specified ItemName does not exist.")
 ;
 ; attributes(no)=attribute name
 ; attributes(no,"value",vno)=attribute value
 ;
 i '$d(attributes) d
 . s name="",attrNo=0
 . f  s name=$o(^MDB(keyId,"domains",domainId,"attribsIndex",name)) q:name=""  d 
 . . s attribId=0
 . . f  s attribId=$o(^MDB(keyId,"domains",domainId,"attribsIndex",name,attribId)) q:attribId=""  d
 . . . i '$d(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId)) q
 . . . s attrNo=attrNo+1
 . . . s attributes(attrNo)=^MDB(keyId,"domains",domainId,"attribs",attribId)
 . . . s valueId="",valueNo=0
 . . . f  s valueId=$o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",valueId)) q:valueId=""  d
 . . . . s value=^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",valueId)
 . . . . s valueNo=valueNo+1
 . . . . i value=$c(31) s value=""
 . . . . s attributes(attrNo,"value",valueNo)=value
 e  d
 . s attrNo=""
 . f  s attrNo=$o(attributes(attrNo)) q:attrNo=""  d
 . . s name=attributes(attrNo)
 . . s attribId=$$getAttributeId(keyId,domainId,name)
 . . i attribId="" q
 . . s valueId="",valueNo=0
 . . f  s valueId=$o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",valueId)) q:valueId=""  d
 . . . s value=^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",valueId)
 . . . s valueNo=valueNo+1
 . . . i value=$c(31) s value=""
 . . . s attributes(attrNo,"value",valueNo)=value
 . . . ;i $g(^zewd("trace")) d trace^%zewdAPI($h_": attributes("_attrNo_",value,"_valueNo_")="_value)
 ;
 i $g(suppressBoxUsage) QUIT ""
 QUIT $$end(startTime,.boxUsage)
 ;
deleteAttributes(keyId,domainName,itemName,attributes,requestId,boxUsage)
 ;
 n attribId,attrNo,domainId,itemId,name,namex,startTime,value,valueId,valueNo,valuex
 ;
 s requestId=$$init(.startTime)
 s keyId=$g(keyId)
 i keyId="" QUIT $$end(startTime,.boxUsage,"MissingParameter","AWSAccessKeyId")
 s domainName=$g(domainName)
 i domainName="" QUIT $$end(startTime,.boxUsage,"MissingParameter","DomainName")
 s domainId=$$getDomainId(keyId,domainName)
 i domainId="" QUIT $$end(startTime,.boxUsage,"NoSuchDomain","The specified domain does not exist.")
 s itemName=$g(itemName)
 i itemName="" QUIT $$end(startTime,.boxUsage,"MissingParameter","ItemName")
 s itemId=$$getItemId(keyId,domainId,itemName)
 i itemId="" QUIT $$end(startTime,.boxUsage,"NoSuchItemName","The specified ItemName does not exist.")
 i '$d(attributes) d
 . ; delete all attributes for this item, first the associated queryIndex records
 . s attribId=""
 . f  s attribId=$o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId)) q:attribId=""  d
 . . s valuex=""
 . . f  s valuex=$o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"valueIndex",valuex)) q:valuex=""  d
 . . . k ^MDB(keyId,"domains",domainId,"queryIndex",attribId,valuex,itemId)
 . k ^MDB(keyId,"domains",domainId,"items",itemId)
 . s namex=$e(itemName,1,$$indexLength())
 . k ^MDB(keyId,"domains",domainId,"itemIndex",namex,itemId)
 e  d
 . ;delete the specified attribute name/value pairs
 . ; attributes(no)=name
 . ; attributes(no,"value",vno)=value
 . s attrNo=""
 . f  s attrNo=$o(attributes(attrNo)) q:attrNo=""  d
 . . s name=$g(attributes(attrNo))
 . . i name="" q
 . . s attribId=$$getAttributeId(keyId,domainId,name)
 . . i attribId="" q
 . . s valueNo=""
 . . f  s valueNo=$o(attributes(attrNo,"value",valueNo)) q:valueNo=""  d
 . . . s value=attributes(attrNo,"value",valueNo)
 . . . s valueId=$$getAttributeValueId(keyId,domainId,itemId,attribId,value)
 . . . i valueId="" q
 . . . ; remove specified value
 . . . s valuex=$e(value,1,$$indexLength())
 . . . k ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"valueIndex",valuex,valueId)
 . . . k ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",valueId)
 . . . k ^MDB(keyId,"domains",domainId,"queryIndex",attribId,valuex,itemId)
 . . ; if no values are left, completely remove attribute from item
 . . i $o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value",""))="" d
 . . . k ^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId,"value")
 . . ; if no references left to this attribute name, remove the attribute name
 . . i '$d(^MDB(keyId,"domains",domainId,"queryIndex",attribId)) d
 . . . s namex=$e(name,1,$$indexLength())
 . . . k ^MDB(keyId,"domains",domainId,"attribs",attribId)
 . . . k ^MDB(keyId,"domains",domainId,"attribsIndex",namex,attribId)
 . ; if no attributes are left, remove the item
 . i $o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",""))="" d
 . . k ^MDB(keyId,"domains",domainId,"items",itemId)
 . . s namex=$e(itemName,1,$$indexLength())
 . . k ^MDB(keyId,"domains",domainId,"itemIndex",namex,itemId)
 . . k ^MDB(keyId,"domains",domainId,"attribs")
 . . k ^MDB(keyId,"domains",domainId,"attribsIndex")
 ;
 QUIT $$end(startTime,.boxUsage)
 ;
query(keyId,domainName,queryExpression,maxNoOfItems,nextToken,itemList,requestId,boxUsage)
 ;
 n context,name,error,startTime,stop,token,value,xvalue
 ;
 i $zv["GT.M" d
 . s context=1
 . i $d(^zewd("config","MGWSI")) s context=0
 s requestId=$$init(.startTime)
 s keyId=$g(keyId)
 i keyId="" QUIT $$end(startTime,.boxUsage,"MissingParameter","keyId")
 s domainName=$g(domainName)
 i domainName="" QUIT $$end(startTime,.boxUsage,"MissingParameter","DomainName")
 s queryExpression=$g(queryExpression)
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("Query Expression="_queryExpression)
 s maxNoOfItems=$g(maxNoOfItems)
 k itemList
 ;
 s error=""
 i $g(nextToken)'="" d  QUIT error
 . n n,pos
 . s pos=$g(^MDB(keyId,"queryResults","nextToken",nextToken))
 . i pos="" s error="InvalidNextToken~The specified next token is not valid" q
 . s n=0,nextToken=""
 . f  s pos=$o(^MDB(keyId,"queryResults","itemList",pos)) q:pos=""  d  q:n=maxNoOfItems
 . . s n=n+1
 . . s itemList(n)=^MDB(keyId,"queryResults","itemList",pos)
 . i pos'="" d
 . . s nextToken=itemList(n)
 . . i $zv["GT.M" d
 . . . s nextToken=$$B64^%ZMGWSIS(nextToken)
 . . e  d
 . . . s nextToken=$$b64Encode^MDBMCache(nextToken)
 . . s ^MDB(keyId,"queryResults","nextToken",nextToken)=pos
 . e  d
 . . k ^MDB(keyId,"queryResults")
 . s error=$$end(startTime,.boxUsage)
 ;
 s nextToken=""
 s error=$$runQuery(keyId,domainName,queryExpression,nextToken,.itemList)
 i error'="" QUIT error
 i $g(maxNoOfItems)>0 d
 . ; add session identifier to stored records
 . k ^MDB(keyId,"queryResults")
 . m ^MDB(keyId,"queryResults","itemList")=itemList
 . ;i queryExpression["sort " s ^MDB(keyId,"queryResults","sorted")=1
 . k itemList
 . s n=0,pos=""
 . f  s pos=$o(^MDB(keyId,"queryResults","itemList",pos)) q:pos=""  d  q:n=maxNoOfItems
 . . s n=n+1
 . . s itemList(n)=^MDB(keyId,"queryResults","itemList",pos)
 . i pos'="" d
 . . s nextToken=itemList(n)
 . . i $zv["GT.M" d
 . . . s nextToken=$$B64^%ZMGWSIS(nextToken,context)
 . . e  d
 . . . s nextToken=$$b64Encode^MDBMCache(nextToken)
 . . s ^MDB(keyId,"queryResults","nextToken",nextToken)=pos
 ;
 QUIT $$end(startTime,.boxUsage)
 ;
 ;
 ;  MDB Server side response to incoming REST requests
 ;
mgwsiResponse(cgi,data)
 ; m_apache HTTP entry point: normalise to WebLink interface
 n %CGIEVAR,n,name,%KEY,unescName
 i $g(^zewd("trace"))=1 k ^mdbcgi m ^mdbcgi=cgi
 i $g(^zewd("trace"))=1 s n=$increment(^mdbdata) m ^mdbdata(n)=data
 i $g(^%zewd("relink"))=1,'$d(^%zewd("relink","process",$j)) s ok=$$relink^%zewdGTMRuntime()
 m %CGIEVAR=cgi
 s name=""
 f  s name=$o(data(name)) q:name=""  d
 . i name="$CONTENT" q
 . s unescName=$$urlDecode(name)
 . s %KEY(unescName)=$$urlDecode($g(data(name,1)))
 . i %KEY(unescName)[$c(13,10) s %KEY(unescName)=$$replace^%zewdAPI(%KEY(unescName),$c(13,10),"")
 d response
 QUIT
 ;
response
 i $d(%KEY) d
 . ; WebLink access entry point here
 . n action,attributes,AWSAcessKeyId,boxUsage,db,error,hash,itemsAndAttrs,keyId
 . n requestId,secretKey,signatureMethod,signatureVersion,stringToSign
 . ;
 . ;k ^rltKey m ^rltKey=%KEY
 . s db=$g(%KEY("db"))
 . i db="" s db="mdb" ;,%KEY("db")=db
 . s action=$g(%KEY("Action"))
 . i $g(^zewd("trace"))=1 d
 . . d trace^%zewdAPI("MDB request processing for "_action_": started at "_$h_"; process: "_$j)
 . . k ^mdbKey m ^mdbKey=%KEY
 . i action="Initialise"!(action="initialise")!(action="Initialize")!(action="initialize") d  QUIT
 . . s %KEY("db")=db
 . . i $d(^MDBUAF("administrator")) d errorResponse("InvalidAction","M DB has already been initialised") q
 . . s error=$$initialise(.requestId,.boxUsage)
 . . i error'="" d errorResponse("InvalidConfigurationFile",error) q
 . . d createResponse(action,requestId,boxUsage)
 . i action="InstallMDBX" d  QUIT
 . . s %KEY("db")=db
 . . s error=$$installMDBX(.requestId,.boxUsage)
 . . i error'="" d errorResponse("InvalidInstallRequest",error) q
 . . d createResponse(action,requestId,boxUsage)
 . i action="InstallMDBM" d  QUIT
 . . s %KEY("db")=db
 . . s error=$$installMDBM(.requestId,.boxUsage)
 . . i error'="" d errorResponse("InvalidInstallRequest",error) q
 . . d createResponse(action,requestId,boxUsage)
 . i $g(%KEY("MDBToken"))'="" d  q:error
 . . n keyId
 . . s error=""
 . . s keyId=$$authenticate^MDBSession(%KEY("MDBToken"))
 . . i keyId="" s error=1 d errorResponse("InvalidTokenId","The Access Token you provided does not exist in our records") QUIT
 . e  d  q:error=1
 . . s error=""
 . . s signatureVersion=+$g(%KEY("SignatureVersion"))
 . . s signatureMethod=$g(%KEY("SignatureMethod"))
 . . s stringToSign=$$createResponseStringToSign(signatureVersion)
 . . s keyId=$g(%KEY("AWSAccessKeyId"))
 . . i keyId="" s keyId=$g(%KEY("MDBAccessKeyId"))
 . . i action="AddUser" d  i error'="" QUIT
 . . . s error=""
 . . . i keyId=$g(^MDBUAF("administrator")) q
 . . . d errorResponse("InvalidAdministratorKey","The Access Key Id was not that of the Administrator")
 . . . s error=1
 . . i keyId="" s error=1 d errorResponse("InvalidClientTokenId","The Access Key Id you provided does not exist in our records") QUIT
 . . i $g(^MDBUAF("keys",keyId))="" s error=1 d errorResponse("InvalidClientTokenId","The Access Key Id you provided does not exist in our records") QUIT
 . . i $g(%KEY("Signature"))="" s error=1 d errorResponse("SignatureDoesNotMatch","The request signature we calculated does not match the signature you provided.  Check your Secret Access Key and signing method.  Consult the service documentation for details") QUIT
 . . s secretKey=$g(^MDBUAF("keys",keyId))
 . . s hash=$$getSignedString(stringToSign,secretKey,signatureMethod)
 . . i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": string to sign:"_stringToSign)
 . . i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": hash="_hash_"; signature rcvd="_%KEY("Signature"))
 . . i hash'=%KEY("Signature") s error=1 d errorResponse("SignatureDoesNotMatch","The request signature we calculated does not match the signature you provided.  Check your Secret Access Key and signing method.  Consult the service documentation for details") QUIT
 . ;
 . ; Security OK
 . ;
 . i $g(^MDBAPI(db,action))'="" d  QUIT
 . . ; Custom extension gateway.  Method should return output in response(lineNo)
 . . ; any error should be returned as errorCode~errorText
 . . n doll,lineNo,method,no,requestId,response,startTime,x
 . . s method=^MDBAPI(db,action)
 . . s x="s error=$$"_method_"(.%KEY,.response)"
 . . s requestId=$$init(.startTime)
 . . x x
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . i $$end(startTime,.boxUsage)
 . . s lineNo=1
 . . i $g(%KEY("mdbRawOutput"))'="true",$g(%KEY("OutputFormat"))'="JSON" d
 . . . s ^CacheTempEWD($j,lineNo)="<?xml version='1.0'?>"_$c(13,10),lineNo=lineNo+1
 . . . s ^CacheTempEWD($j,lineNo)="<"_action_"Response xmlns=""http://"_db_".mgateway.com/doc/2009-06-05/"">",lineNo=lineNo+1
 . . s no=""
 . . f  s no=$o(response(no)) q:no=""  d
 . . . s ^CacheTempEWD($j,lineNo)=response(no),lineNo=lineNo+1
 . . d createResponse(db_":"_action,requestId,boxUsage) QUIT
 . ;
 . i db="mdb" s %KEY("db")="mdb"
 . i action="AddUser" d  QUIT
 . . n userKeyId,userSecretKey
 . . s userKeyId=$g(%KEY("UserAccessKeyId"))
 . . i userKeyId="" d errorResponse("InvalidKeyId","The new user Access Key Id was not defined") QUIT
 . . s userSecretKey=$g(%KEY("UserSecretKey"))
 . . i userSecretKey="" d errorResponse("InvalidSecretKey","The new user Secret Key was not defined") QUIT
 . . s error=$$addUser(userKeyId,userSecretKey,.requestId,.boxUsage)
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . d createResponse(action,requestId,boxUsage) QUIT
 . ;
 . i action="GetToken" d  QUIT
 . . n boxUsage,requestId,startTime,token
 . . s requestId=$$init(.startTime)
 . . s token=$$createNewSession^MDBSession(keyId,1200)
 . . i $$end(startTime,.boxUsage)
 . . d createResponse(action,requestId,boxUsage)
 . ;
 . i action="CreateDomain" d  QUIT
 . . n domainName,resp
 . . s domainName=$g(%KEY("DomainName"))
 . . s error=$$createDomain(keyId,domainName,.requestId,.boxUsage)
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . d createResponse(action,requestId,boxUsage) QUIT
 . ;
 . i action="DeleteDomain" d  QUIT
 . . n domainName,resp
 . . s domainName=$g(%KEY("DomainName"))
 . . s error=$$deleteDomain(keyId,domainName,.requestId,.boxUsage)
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . d createResponse(action,requestId,boxUsage) QUIT
 . ;
 . i action="ListDomains" d  QUIT
 . . n domainList,maxNoOfDomains,nextToken,resp
 . . s maxNoOfDomains=$g(%KEY("MaxNumberOfDomains"))
 . . s nextToken=$g(%KEY("NextToken"))
 . . s error=$$listDomains(keyId,maxNoOfDomains,.nextToken,.domainList,.requestId,.boxUsage)
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . d createResponse(action,requestId,boxUsage) QUIT
 . ;
 . i action="DomainMetadata" d  QUIT
 . . n domainName,metaData
 . . s domainName=$g(%KEY("DomainName"))
 . . s error=$$domainMetadata(keyId,domainName,.metaData,.requestId,.boxUsage)
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . d createResponse(action,requestId,boxUsage) QUIT
 . ;
 . i action="PutAttributes" d  QUIT
 . . n domainName,error,i,itemName,name,paramName,paramValue,replace,start,stop
 . . s domainName=$g(%KEY("DomainName"))
 . . s itemName=$g(%KEY("ItemName"))
 . . s stop=0
 . . s name=$o(%KEY("Attribute."))
 . . s start=$p(name,".",2)
 . . s error=""
 . . f i=start:1 d  q:stop
 . . . s paramName="Attribute."_i_".Name"
 . . . i '$d(%KEY(paramName)) s stop=1 q
 . . . s paramValue="Attribute."_i_".Value"
 . . . ;i '$d(%KEY(paramValue)) s stop=1 q
 . . . i '$d(%KEY(paramValue)) s error="MissingParameter~Attribute Value missing for Attribute Name='"_%KEY(paramName),stop=1 q
 . . . ;i %KEY(paramValue)="" s error="MissingParameter~Attribute Value missing for Attribute Name='"_%KEY(paramName),stop=1
 . . . s replace=$g(%KEY("Attribute."_i_".Replace"))
 . . . i replace="true" s replace=1
 . . . ; attributes(no,"name")=attribute name
 . . . ; attributes(no,"value")=attribute value
 . . . ; attributes(no,"replace")=1
 . . . s attributes(i,"name")=%KEY(paramName)
 . . . s attributes(i,"value")=%KEY(paramValue)
 . . . i replace s attributes(i,"replace")=1
 . . i error="" s error=$$putAttributes(keyId,domainName,itemName,.attributes,.requestId,.boxUsage)
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . d createResponse(action,requestId,boxUsage) QUIT
 . ;
 . i action="GetAttributes" d  QUIT
 . . n domainName,error,i,itemName,name,paramName,paramValue,replace,start,stop
 . . s domainName=$g(%KEY("DomainName"))
 . . s itemName=$g(%KEY("ItemName"))
 . . s stop=0
 . . s name=$o(%KEY("AttributeName."))
 . . s start=$p(name,".",2)
 . . s error=""
 . . k attributes
 . . f i=start:1 d  q:stop
 . . . s paramName="AttributeName."_i
 . . . i '$d(%KEY(paramName)) s stop=1 q
 . . . s attributes(i)=%KEY(paramName)
 . . . ;s attributes(1)="test"
 . . s error=$$getAttributes(keyId,domainName,itemName,.attributes,.requestId,.boxUsage)
 . . ; attributes(no)=attribute name
 . . ; attributes(no,"value",vno)=attribute value
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . d createResponse(action,requestId,boxUsage)
 . ;
 . i action="DeleteAttributes" d  QUIT
 . . n attributes,domainName,error,i,itemName,name,paramName,paramValue,start,stop
 . . s domainName=$g(%KEY("DomainName"))
 . . s itemName=$g(%KEY("ItemName"))
 . . s stop=0
 . . s name=$o(%KEY("Attribute."))
 . . s start=$p(name,".",2)
 . . s error=""
 . . ; attributes(no)=name
 . . ; attributes(no,"value",vno)=value
 . . k attributes
 . . f i=start:1 d  q:stop
 . . . s paramName="Attribute."_i_".Name"
 . . . i '$d(%KEY(paramName)) s stop=1 q
 . . . s name=%KEY(paramName)
 . . . s attributes(i)=name
 . . . s paramValue="Attribute."_i_".Value"
 . . . i '$d(%KEY(paramValue)) s stop=1 q
 . . . i %KEY(paramValue)="" s error="MissingParameter~Attribute Value missing for Attribute Name='"_%KEY(paramName),stop=1
 . . . s attributes(i,"value",1)=%KEY(paramValue)
 . . i error="" s error=$$deleteAttributes(keyId,domainName,itemName,.attributes,.requestId,.boxUsage)
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . d createResponse(action,requestId,boxUsage) QUIT
 . ;
 . i action="Query" d  QUIT
 . . n domainName,error,itemList,maxNoOfItems,nextToken,queryExpression
 . . s domainName=$g(%KEY("DomainName"))
 . . s queryExpression=$g(%KEY("QueryExpression"))
 . . s maxNoOfItems=$g(%KEY("MaxNumberOfItems"))
 . . s nextToken=$g(%KEY("NextToken"))
 . . s error=$$query(keyId,domainName,queryExpression,maxNoOfItems,.nextToken,.itemList,.requestId,.boxUsage)
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . d createResponse(action,requestId,boxUsage) QUIT
 . ;
 . i action="QueryWithAttributes" d  QUIT
 . . n attribs,bx,domainName,error,ex,i,name,itemList,itemName,itemsAndAttrs,maxNoOfItems,name,nextToken
 . . n paramName,pos,queryExpression,rx,sorted,start,stop,sub
 . . ;
 . . s domainName=$g(%KEY("DomainName"))
 . . s queryExpression=$g(%KEY("QueryExpression"))
 . . s maxNoOfItems=$g(%KEY("MaxNumberOfItems"))
 . . s nextToken=$g(%KEY("NextToken"))
 . . s name=$o(%KEY("AttributeName."))
 . . s start=$p(name,".",2)
 . . s error="",stop=0
 . . f i=start:1 d  q:stop
 . . . s paramName="AttributeName."_i
 . . . i '$d(%KEY(paramName)) s stop=1 q
 . . . s attributes(i)=%KEY(paramName)
 . . s error=$$query(keyId,domainName,queryExpression,maxNoOfItems,.nextToken,.itemList,.requestId,.boxUsage)
 . . s pos=""
 . . f  s pos=$o(itemList(pos)) q:pos=""  d
 . . . s itemName=itemList(pos)
 . . . k attribs
 . . . m attribs=attributes
 . . . s ex=$$getAttributes(keyId,domainName,itemName,.attribs,.rx,.bx,1)
 . . . m itemsAndAttrs(pos)=attribs
 . . . s itemsAndAttrs(pos)=itemName
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . d createResponse(action,requestId,boxUsage) QUIT
 . ;
 . i action="Select" d  QUIT
 . . n attribs,attributes,boxUsage,bx,domainName,error,ex,itemList,itemName,nextToken
 . . n pos,requestId,rx,selectExpression,startTime
 . . ;
 . . s requestId=$$init(.startTime)
 . . s selectExpression=$g(%KEY("SelectExpression"))
 . . s nextToken=$g(%KEY("NextToken"))
 . . i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": Action Select, entering runSelect. keyId="_keyId_"; selectExpression="_selectExpression)
 . . s error=$$runSelect(keyId,selectExpression,.itemList,.attributes,.domainName)
 . . i $g(^zewd("trace")) d trace^%zewdAPI($h_": finished runSelect")
 . . i error'="" d errorResponse($p(error,"~",1),$p(error,"~",2)) QUIT
 . . i $g(attributes)="count(*)" d
 . . . n count
 . . . s itemsAndAttrs(1)="Domain"
 . . . s itemsAndAttrs(1,1)="Count"
 . . . s pos="",count=0
 . . . f  s pos=$o(itemList(pos)) q:pos=""  s count=count+1
 . . . s itemsAndAttrs(1,1,"value",1)=count
 . . e  d
 . . . s pos=""
 . . . ;i $g(^zewd("trace")) d trace^%zewdAPI($h_": pos=''")
 . . . f  s pos=$o(itemList(pos)) q:pos=""  d
 . . . . ;i $g(^zewd("trace")) d trace^%zewdAPI($h_": pos="_pos)
 . . . . s itemName=itemList(pos)
 . . . . k attribs
 . . . . m attribs=attributes
 . . . . ;i $g(^zewd("trace")) d trace^%zewdAPI($h_": calling getAttributes for keyId="_keyId_"; domainName="_domainName_"; itemName "_itemName)
 . . . . s ex=$$getAttributes(keyId,domainName,itemName,.attribs,.rx,.bx,1)
 . . . . ;i $g(^zewd("trace")) d trace^%zewdAPI($h_": finished getAttributes")
 . . . . m itemsAndAttrs(pos)=attribs
 . . . . s itemsAndAttrs(pos)=itemName
 . . ;i $g(^zewd("trace")) d trace^%zewdAPI($h_": about to call $$end")
 . . i $$end(startTime,.boxUsage)
 . . ;i $g(^zewd("trace")) d trace^%zewdAPI($h_": about to start createResponse")
 . . d createResponse(action,requestId,boxUsage) QUIT
 . ;
 . d errorResponse("InvalidAction","The action "_action_" is not valid for this web service") QUIT
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("MDB request processing ended at "_$h)
 QUIT
 ;
createResponse(action,requestId,boxUsage)
 ;
 n len,lineNo
 ;
 ;i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": Commencing createResponse")
 i '$d(%KEY("isCSP")) d
 . w "HTTP/1.0 200 OK"_$c(13,10)
 . w "Date: "_$$inetDate^%zewdAPI($h)_" "_$tr($g(^MDBConfig("GMTOffset")),":","")_$c(13,10)
 . i $g(%KEY("OutputFormat"))="JSON" d
 . . w "Content-type: application/json"_$c(13,10)
 . e  d
 . . w "Content-type: text/xml"_$c(13,10)
 s lineNo=1
 i $g(%KEY("OutputFormat"))'="JSON" d
 . i $g(%KEY("db"))="mdb" d
 . . n apiVersion
 . . k ^CacheTempEWD($j)
 . . s lineNo=1
 . . s ^CacheTempEWD($j,lineNo)="<?xml version='1.0'?>"_$c(13,10),lineNo=lineNo+1
 . . s apiVersion=$g(%KEY("Version")) i apiVersion="" s apiVersion="2009-04-15"
 . . s ^CacheTempEWD($j,lineNo)="<"_action_"Response xmlns=""http://sdb.amazonaws.com/doc/"_apiVersion_"/"">",lineNo=lineNo+1
 . e  d
 . . s action=$p(action,":",2)
 . . s lineNo=$o(^CacheTempEWD($j,""),-1)+1
 ;
 i action="GetToken" d
 . i $g(%KEY("OutputFormat"))="JSON" d
 . . s ^CacheTempEWD($j,lineNo)="{token:"""_token_"""}"
 . e  d
 . . s ^CacheTempEWD($j,lineNo)="<GetTokenResult>"_token_"</GetTokenResult>",lineNo=lineNo+1
 ;
 i action="ListDomains" d
 . i '$d(domainList) d
 . . i $g(%KEY("OutputFormat"))="JSON" d
 . . . s ^CacheTempEWD($j,lineNo)="[]",lineNo=lineNo+1
 . . e  d
 . . . s ^CacheTempEWD($j,lineNo)="<ListDomainsResult />",lineNo=lineNo+1
 . e  d
 . . n comma,no
 . . i $g(%KEY("OutputFormat"))="JSON" d
 . . . s ^CacheTempEWD($j,lineNo)="[",lineNo=lineNo+1
 . . . s no="",comma=""
 . . . f  s no=$o(domainList(no)) q:no=""  d
 . . . . s ^CacheTempEWD($j,lineNo)=comma_""""_domainList(no)_"""",lineNo=lineNo+1,comma=","
 . . . s ^CacheTempEWD($j,lineNo)="]",lineNo=lineNo+1
 . . e  d
 . . . s ^CacheTempEWD($j,lineNo)="<ListDomainsResult>",lineNo=lineNo+1
 . . . s no=""
 . . . f  s no=$o(domainList(no)) q:no=""  d
 . . . . s ^CacheTempEWD($j,lineNo)="<DomainName>"_domainList(no)_"</DomainName>",lineNo=lineNo+1
 . . . i $g(nextToken)'="" s ^CacheTempEWD($j,lineNo)="<NextToken>"_nextToken_"</NextToken>",lineNo=lineNo+1
 . . . s ^CacheTempEWD($j,lineNo)="</ListDomainsResult>",lineNo=lineNo+1
 ;
 i action="CreateDomain",$g(%KEY("OutputFormat"))="JSON" d
 . s ^CacheTempEWD($j,lineNo)="{ok:true}",lineNo=lineNo+1
 ;
 i action="DeleteAttributes",$g(%KEY("OutputFormat"))="JSON" d
 . s ^CacheTempEWD($j,lineNo)="{ok:true}",lineNo=lineNo+1
 ;
 i action="DeleteDomain",$g(%KEY("OutputFormat"))="JSON" d
 . s ^CacheTempEWD($j,lineNo)="{ok:true}",lineNo=lineNo+1
 ;
 i action="PutAttributes",$g(%KEY("OutputFormat"))="JSON" d
 . s ^CacheTempEWD($j,lineNo)="{ok:true}",lineNo=lineNo+1
 ;
 i action="DomainMetadata" d
 . i '$d(metaData) d
 . . i $g(%KEY("OutputFormat"))="JSON" d
 . . . s ^CacheTempEWD($j,lineNo)="{}",lineNo=lineNo+1
 . . e  d
 . . . s ^CacheTempEWD($j,lineNo)="<DomainMetadataResult />",lineNo=lineNo+1
 . e  d
 . . n comma,name
 . . i $g(%KEY("OutputFormat"))="JSON" d
 . . . s ^CacheTempEWD($j,lineNo)="{",lineNo=lineNo+1
 . . . s name="",comma=""
 . . . f  s name=$o(metaData(name)) q:name=""  d
 . . . . s ^CacheTempEWD($j,lineNo)=comma_name_":"_metaData(name),lineNo=lineNo+1,comma=","
 . . . s ^CacheTempEWD($j,lineNo)="}",lineNo=lineNo+1
 . . e  d
 . . . s ^CacheTempEWD($j,lineNo)="<DomainMetadataResult>",lineNo=lineNo+1
 . . . s name=""
 . . . f  s name=$o(metaData(name)) q:name=""  d
 . . . . s ^CacheTempEWD($j,lineNo)="<"_name_">"_metaData(name)_"</"_name_">",lineNo=lineNo+1
 . . . s ^CacheTempEWD($j,lineNo)="</DomainMetadataResult>",lineNo=lineNo+1
 ;
 i action="GetAttributes",%KEY("db")="mdb" d
 . i '$d(attributes) d
 . . i $g(%KEY("OutputFormat"))="JSON" d
 . . . s ^CacheTempEWD($j,lineNo)="{}",lineNo=lineNo+1
 . . e  d
 . . . s ^CacheTempEWD($j,lineNo)="<GetAttributesResult />",lineNo=lineNo+1
 . e  d
 . . n acomma,comma,count,name,no,quote,stop,value,valueNo
 . . i $g(%KEY("OutputFormat"))="JSON" d
 . . . s ^CacheTempEWD($j,lineNo)="{",lineNo=lineNo+1
 . . . s no="",comma=""
 . . . f  s no=$o(attributes(no)) q:no=""  d
 . . . . s name=attributes(no)
 . . . . s valueNo="",count=0,stop=0
 . . . . f  s valueNo=$o(attributes(no,"value",valueNo)) q:valueNo=""  d  q:stop
 . . . . . s count=count+1 i count>1 s stop=1
 . . . . i count=1 d
 . . . . . s ^CacheTempEWD($j,lineNo)=comma_name_":",acomma="",comma=",",lineNo=lineNo+1
 . . . . e  d
 . . . . . s ^CacheTempEWD($j,lineNo)=comma_name_":[",acomma="",comma=",",lineNo=lineNo+1
 . . . . s valueNo=""
 . . . . f  s valueNo=$o(attributes(no,"value",valueNo)) q:valueNo=""  d
 . . . . . s value=attributes(no,"value",valueNo)
 . . . . . s quote="""" i $$numeric(value) s quote=""
 . . . . . s ^CacheTempEWD($j,lineNo)=acomma_quote_value_quote,lineNo=lineNo+1,acomma=","
 . . . . i count>1 s ^CacheTempEWD($j,lineNo)="]",lineNo=lineNo+1
 . . . s ^CacheTempEWD($j,lineNo)="}",lineNo=lineNo+1
 . . e  d
 . . . s ^CacheTempEWD($j,lineNo)="<GetAttributesResult>",lineNo=lineNo+1
 . . . s no=""
 . . . f  s no=$o(attributes(no)) q:no=""  d
 . . . . s name=attributes(no)
 . . . . s valueNo=""
 . . . . f  s valueNo=$o(attributes(no,"value",valueNo)) q:valueNo=""  d
 . . . . . s value=attributes(no,"value",valueNo)
 . . . . . s value=$$escape(value)
 . . . . . s ^CacheTempEWD($j,lineNo)="<Attribute>",lineNo=lineNo+1
 . . . . . s ^CacheTempEWD($j,lineNo)="<Name>"_name_"</Name>",lineNo=lineNo+1
 . . . . . s ^CacheTempEWD($j,lineNo)="<Value>"_value_"</Value>",lineNo=lineNo+1
 . . . . . s ^CacheTempEWD($j,lineNo)="</Attribute>",lineNo=lineNo+1
 . . . s ^CacheTempEWD($j,lineNo)="</GetAttributesResult>",lineNo=lineNo+1
 ;
 i action="Select" d
 . ;i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": action=Select")
 . i '$d(itemsAndAttrs) d  q
 . . s ^CacheTempEWD($j,lineNo)="<SelectResult />",lineNo=lineNo+1
 . e  d
 . . n attrName,attrNo,attrValue,attrValueNo,itemName,itemNo
 . . s ^CacheTempEWD($j,lineNo)="<SelectResult>",lineNo=lineNo+1
 . . s itemNo=""
 . . f  s itemNo=$o(itemsAndAttrs(itemNo)) q:itemNo=""  d
 . . . s itemName=itemsAndAttrs(itemNo)
 . . . s ^CacheTempEWD($j,lineNo)="<Item>",lineNo=lineNo+1
 . . . s ^CacheTempEWD($j,lineNo)="<Name>"_itemName_"</Name>",lineNo=lineNo+1
 . . . s attrNo=""
 . . . f  s attrNo=$o(itemsAndAttrs(itemNo,attrNo)) q:attrNo=""  d
 . . . . s attrName=itemsAndAttrs(itemNo,attrNo)
 . . . . s attrValueNo=""
 . . . . f  s attrValueNo=$o(itemsAndAttrs(itemNo,attrNo,"value",attrValueNo)) q:attrValueNo=""  d
 . . . . . s attrValue=itemsAndAttrs(itemNo,attrNo,"value",attrValueNo)
 . . . . . s attrValue=$$escape(attrValue)
 . . . . . s ^CacheTempEWD($j,lineNo)="<Attribute>",lineNo=lineNo+1
 . . . . . s ^CacheTempEWD($j,lineNo)="<Name>"_attrName_"</Name><Value>"_attrValue_"</Value>",lineNo=lineNo+1
 . . . . . s ^CacheTempEWD($j,lineNo)="</Attribute>",lineNo=lineNo+1
 . . . s ^CacheTempEWD($j,lineNo)="</Item>",lineNo=lineNo+1
 . . s ^CacheTempEWD($j,lineNo)="</SelectResult>",lineNo=lineNo+1
 . ;i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": finished action=Select")
 ;
 i action="Query" d
 . i '$d(itemList) d
 . . i $g(%KEY("OutputFormat"))="JSON" d
 . . . s ^CacheTempEWD($j,lineNo)="[]",lineNo=lineNo+1
 . . e  d
 . . . s ^CacheTempEWD($j,lineNo)="<QueryResult />",lineNo=lineNo+1
 . e  d
 . . n comma,position,quotes
 . . i $g(%KEY("OutputFormat"))="JSON" d
 . . . s ^CacheTempEWD($j,lineNo)="[",lineNo=lineNo+1
 . . . s position="",comma=""
 . . . f  s position=$o(itemList(position)) q:position=""  d
 . . . . s quotes="""" i $$numeric(itemList(position)) s quotes=""
 . . . . s ^CacheTempEWD($j,lineNo)=comma_quotes_itemList(position)_quotes,lineNo=lineNo+1,comma=","
 . . . s ^CacheTempEWD($j,lineNo)="]",lineNo=lineNo+1
 . . e  d
 . . . n position
 . . . s ^CacheTempEWD($j,lineNo)="<QueryResult>",lineNo=lineNo+1
 . . . s position=""
 . . . f  s position=$o(itemList(position)) q:position=""  d
 . . . . s ^CacheTempEWD($j,lineNo)="<ItemName>"_itemList(position)_"</ItemName>",lineNo=lineNo+1
 . . . i $g(nextToken)'="" s ^CacheTempEWD($j,lineNo)="<NextToken>"_nextToken_"</NextToken>",lineNo=lineNo+1
 . . . s ^CacheTempEWD($j,lineNo)="</QueryResult>",lineNo=lineNo+1
 ;
 i action="QueryWithAttributes" d
 . i '$d(itemsAndAttrs) d  q
 . . s ^CacheTempEWD($j,lineNo)="<QueryWithAttributesResult />",lineNo=lineNo+1
 . e  d
 . . n attrName,attrNo,attrValue,attrValueNo,itemName,itemNo
 . . s ^CacheTempEWD($j,lineNo)="<QueryWithAttributesResult>",lineNo=lineNo+1
 . . s itemNo=""
 . . f  s itemNo=$o(itemsAndAttrs(itemNo)) q:itemNo=""  d
 . . . s itemName=itemsAndAttrs(itemNo)
 . . . s ^CacheTempEWD($j,lineNo)="<Item>",lineNo=lineNo+1
 . . . s ^CacheTempEWD($j,lineNo)="<Name>"_itemName_"</Name>",lineNo=lineNo+1
 . . . s attrNo=""
 . . . f  s attrNo=$o(itemsAndAttrs(itemNo,attrNo)) q:attrNo=""  d
 . . . . s attrName=itemsAndAttrs(itemNo,attrNo)
 . . . . s attrValueNo=""
 . . . . f  s attrValueNo=$o(itemsAndAttrs(itemNo,attrNo,"value",attrValueNo)) q:attrValueNo=""  d
 . . . . . s attrValue=itemsAndAttrs(itemNo,attrNo,"value",attrValueNo)
 . . . . . s attrValue=$$escape(attrValue)
 . . . . . s ^CacheTempEWD($j,lineNo)="<Attribute>",lineNo=lineNo+1
 . . . . . s ^CacheTempEWD($j,lineNo)="<Name>"_attrName_"</Name><Value>"_attrValue_"</Value>",lineNo=lineNo+1
 . . . . . s ^CacheTempEWD($j,lineNo)="</Attribute>",lineNo=lineNo+1
 . . . s ^CacheTempEWD($j,lineNo)="</Item>",lineNo=lineNo+1
 . . s ^CacheTempEWD($j,lineNo)="</QueryWithAttributesResult>",lineNo=lineNo+1
 ;
 i $g(%KEY("db"))="mdb",$g(%KEY("OutputFormat"))'="JSON" d
 . s ^CacheTempEWD($j,lineNo)="<ResponseMetadata>",lineNo=lineNo+1
 . s ^CacheTempEWD($j,lineNo)="<RequestId>"_$g(requestId)_"</RequestId>",lineNo=lineNo+1
 . s ^CacheTempEWD($j,lineNo)="<BoxUsage>"_$g(boxUsage)_"</BoxUsage>",lineNo=lineNo+1
 . s ^CacheTempEWD($j,lineNo)="</ResponseMetadata>",lineNo=lineNo+1
 i $g(%KEY("mdbRawOutput"))'="true",$g(%KEY("OutputFormat"))'="JSON" d
 . s ^CacheTempEWD($j,lineNo)="</"_action_"Response>"_$c(13,10),lineNo=lineNo+1
 ;
 s len=0,lineNo=""
 f  s lineNo=$o(^CacheTempEWD($j,lineNo)) q:lineNo=""  d
 . s len=len+$l(^CacheTempEWD($j,lineNo))
 i '$d(%KEY("isCSP")) w "Content-length: "_len_$c(13,10,13,10) 
 s lineNo=""
 f  s lineNo=$o(^CacheTempEWD($j,lineNo)) q:lineNo=""  d
 . w ^CacheTempEWD($j,lineNo)
 k ^CacheTempEWD($j)
 w !
 ;i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": finished createResponse")
 QUIT
 ;
errorResponse(ec,em)
 ;
 n resp
 ;
 i $g(%KEY("OutputFormat"))="JSON" d  QUIT
 . s resp="400 Bad Request"
 . i ec="SignatureDoesNotMatch" s resp="403 Forbidden"
 . w "HTTP/1.0 "_resp_$c(13,10)
 . w "Date: "_$$inetDate^%zewdAPI($h)_" "_$tr($g(^MDBConfig("GMTOffset")),":","")_$c(13,10)
 . w "Content-type: application/json"_$c(13,10),$c(13,10)
 . w "{""ErrorCode"":"""_ec_""",""ErrorMessage"":"""_em_"""}"_$c(13,10)
 . w !
 ;
 s resp="400 Bad Request"
 i ec="SignatureDoesNotMatch" s resp="403 Forbidden"
 w "HTTP/1.0 "_resp_$c(13,10)
 w "Date: "_$$inetDate^%zewdAPI($h)_" "_$tr($g(^MDBConfig("GMTOffset")),":","")_$c(13,10)
 w "Content-type: text/xml"_$c(13,10),$c(13,10)
 w "<?xml version='1.0'?>"_$c(13,10)
 w "<Response><Errors><Error>"
 w "<Code>"_ec_"</Code>"
 w "<Message>"_em_"</Message>"
 i ec'="SignatureDoesNotMatch",ec'="InvalidClientTokenId" w "<BoxUsage>0</BoxUsage>"
 w "</Error></Errors>"
 ; Note mis-spelling of ID instead of Id to follow SimpleDB's "feature"!
 w "<RequestID>"_$$createRequestId()_"</RequestID>"
 w "</Response>"_$c(13,10)
 w !
 QUIT
 ;
createResponseStringToSign(version)
 ;
 n amp,n,name,nlc,stringToSign,nvpListlc,value
 ;
 s stringToSign=""
 ;
 i version=0 d  QUIT stringToSign
 . s stringToSign=$g(%KEY("Action"))_$g(%KEY("Timestamp"))
 ;
 i version=1 d  QUIT stringToSign
 . s n=""
 . f  s n=$o(%KEY(n)) q:n=""  d
 . . q:$e(n,1,3)="MGW"
 . . q:n="Signature"
 . . q:n="isCSP"
 . . s nvpListlc($$zcvt^%zewdAPI(n,"l"))=n
 . . ;i $zv["GT.M" d
 . . ;. s nvpListlc($$zcvt^%zewdAPI(n,"l"))=n
 . . ;e  d
 . . ;. s nvpListlc($zcvt(n,"l"))=n
 . s nlc=""
 . f  s nlc=$o(nvpListlc(nlc)) q:nlc=""  d
 . . s name=nvpListlc(nlc)
 . . s value=%KEY(name)
 . . s stringToSign=stringToSign_name_value
 ;
 i version=2 d  QUIT stringToSign
 . n location,method,url
 . s name="",amp=""
 . f  s name=$o(%KEY(name)) q:name=""  d
 . . q:$e(name,1,3)="MGW"
 . . q:name="Signature"
 . . q:name="isCSP"
 . . s value=$$urlEscape(%KEY(name))
 . . s stringToSign=stringToSign_amp_name_"="_value
 . . s amp="&"
 . s method=$g(%CGIEVAR("REQUEST_METHOD"))
 . s url=$g(%CGIEVAR("SERVER_NAME"))
 . s location=$g(%CGIEVAR("REQUEST_URI"))
 . i location["?" s location=$p(location,"?",1)
 . i location="" d
 . . s location="/scripts/mgwms32.dll"
 . . i $d(%KEY("isCSP")) s location=$$baseUri^MDBMCache()
 . e  d
 . . i location["http://"!(location["https://") d
 . . . s location=$p(location,"://",2)
 . . . s location="/"_$p(location,"/",2,2000)
 . . . s location=$p(location,"?",1)
 . s stringToSign=method_$c(10)_url_$c(10)_location_$c(10)_stringToSign
 . ;s stringToSign=$$replaceAll^%zewdAPI(stringToSign,$c(13,10),"")
 ;
 QUIT stringToSign
 ;
createToken(length)
 ;
 n i,string,token
 ;
 s string="ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz1234567890"
 s token=""
 f i=1:1:length s token=token_$e(string,($r($l(string))+1))
 QUIT token_"="
 ;
init(startTime)
 ;
 i $g(^zewd("trace")) d trace^%zewdAPI($h_": $$init")
 i $zv["GT.M" d
 . s startTime=$$ZTS^%ZMGWSIS(1)
 e  d
 . s startTime=$zts
 s startTime=(startTime*86400)+$p(startTime,",",2)
 QUIT $$createRequestId()
 ;
createRequestId()
 n hex,i,responseId
 s responseId="" 
 f i=1:1:16 d
 . s hex=$$hex($r(256))
 . i $l(hex)=1 s hex=0_hex
 . s responseId=responseId_hex
 . i i=4!(i=6)!(i=8)!(i=10) s responseId=responseId_"-"
 QUIT $$zcvt^%zewdAPI(responseId,"l")
 ;i $zv["GT.M" QUIT $$zcvt^%zewdAPI(responseId,"l")
 ;QUIT $zcvt(responseId,"l")
end(startTime,boxUsage,errorCode,parameter1,parameter2)
 ;
 n endTime,error
 ;
 i $g(^zewd("trace")) d trace^%zewdAPI($h_": $$end")
 i $zv["GT.M" d
 . s endTime=$$ZTS^%ZMGWSIS(1)
 e  d
 . s endTime=$zts
 s endTime=(endTime*86400)+$p(endTime,",",2)
 s boxUsage=endTime-startTime,boxUsage=$j(boxUsage,1,10)
 i $g(errorCode)="" QUIT ""
 s error=$g(^MDBErrors(errorCode))
 i error="" QUIT errorCode_"~"_$g(parameter1)
 i $g(parameter1)'="" s $p(error,"~",2)=parameter1
 i $g(parameter2)'="" s $p(error,"~",4)=parameter2
 QUIT errorCode_"~"_$tr(error,"~","")
 ;
getSignedString(string,secretKey,signatureMethod)
 ;
 n context,hash,returnValue
 ;
 i $zv["GT.M" d  QUIT returnValue
 . s context=1
 . i $d(^zewd("config","MGWSI")) s context=0
 . i $$zcvt^%zewdAPI($g(signatureMethod),"l")="hmacsha256" d
 . . s returnValue=$$HMACSHA256^%ZMGWSIS(string,secretKey,1,context)
 . e  d
 . . s returnValue=$$HMACSHA1^%ZMGWSIS(string,secretKey,1,context)
 ;
 QUIT $$sign^MDBMCache(signatureMethod,string,secretKey)
 ;
runQuery(keyId,domainName,queryExpression,nextToken,itemList)
 ;
 n domainId,error,filter,itemId,itemName,matchList,name,no,pos,stop
 ;
 ;i $g(^zewd("trace")) d trace^%zewdAPI($h_": runQuery started")
 s error=""
 s filter="",stop=0
 i queryExpression["itemName{}" s queryExpression=$$replaceAll^%zewdAPI(queryExpression,"itemName{}","itemName()")
 s queryExpression=$$stripSpaces($g(queryExpression))
 s domainId=$$getDomainId(keyId,domainName)
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("runQuery: domainId="_domainId_"; queryExpression="_queryExpression)
 i domainId="" QUIT ""
 ;
 i queryExpression="" d  QUIT error
 . s itemId="",no=0
 . f  s itemId=$o(^MDB(keyId,"domains",domainId,"items",itemId)) q:itemId=""  d
 . . s no=no+1
 . . s itemList(no)=^MDB(keyId,"domains",domainId,"items",itemId)
 ;
 i queryExpression["sort " d  i error'="" QUIT error
 . i queryExpression["union "!(queryExpression["union[")!(queryExpression["not ")!(queryExpression["not[") s error=$$queryError(1)
 ;
 f  d  q:stop  q:queryExpression=""  q:error'=""
 . k matchList
 . s error=$$queryPredicate(.queryExpression,keyId,domainId,.matchList,.name)
 . q:error'=""
 . i filter="" d mergeLists(.itemList,.matchList) ;m itemList=matchList
 . i filter="intersection" d
 . . s pos=""
 . . f  s pos=$o(itemList(pos)) q:pos=""  d
 . . . s itemName=itemList(pos)
 . . . i '$$inMatchList(.matchList,itemName) k itemList(pos)
 . . . ;i '$d(matchList(itemId)) k itemList(itemId)
 . i filter="union" d mergeLists(.itemList,.matchList) ;m itemList=matchList
 . q:queryExpression=""
 . s filter=""
 . i $$startsWith(queryExpression,"intersection") d  q
 . . s queryExpression=$e(queryExpression,13,$l(queryExpression))
 . . s queryExpression=$$stripSpaces(queryExpression)
 . . s filter="intersection"
 . i $$startsWith(queryExpression,"union") d  q
 . . s queryExpression=$e(queryExpression,6,$l(queryExpression))
 . . s queryExpression=$$stripSpaces(queryExpression)
 . . s filter="union"
 . i $$startsWith(queryExpression,"sort") s stop=1 q
 . s error=$$queryError(2)
 ;
 i $$startsWith(queryExpression,"sort") d
 . n attrNo,direction,found,itemName,itemId,n,sortAttr,sortAttrId,sortedList,xvalue
 . s queryExpression=$e(queryExpression,5,$l(queryExpression))
 . s queryExpression=$$stripSpaces(queryExpression)
 . s sortAttr=$p(queryExpression,"'",2)
 . i sortAttr="" s error=$$queryError(3) q
 . s sortAttrId=$$getAttributeId(keyId,domainId,sortAttr)
 . i sortAttrId="" s error=$$queryError(4) q
 . i $g(name)'="",name'=sortAttr s error=$$queryError(5) q
 . i $g(name)="" d  q:error'=""
 . . n no,ok
 . . s no="",ok=0
 . . f  s no=$o(name(no)) q:no=""  d  q:ok
 . . . i name(no)=sortAttr s ok=1 q
 . . i 'ok s error=$$queryError(6)
 . s direction="1"
 . s queryExpression=$p(queryExpression,"'",3)
 . i queryExpression["desc" s direction="-1"
 . s pos=""
 . ;f  s pos=$o(itemList(pos)) q:pos=""  d
 . ;. s itemName=itemList(pos)
 . ;. s itemNamex=$e(itemName,1,$$indexLength())
 . ;. s sortedList(itemNamex,pos)=itemName
 . s pos=""
 . f  s pos=$o(itemList(pos)) q:pos=""  d
 . . s itemName=itemList(pos)
 . . s itemId=$$getItemId(keyId,domainId,itemName)
 . . s attrNo=""
 . . f  s attrNo=$o(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",sortAttrId,"value",attrNo)) q:attrNo=""  d
 . . . s xvalue=^MDB(keyId,"domains",domainId,"items",itemId,"attribs",sortAttrId,"value",attrNo)
 . . . s sortedList(xvalue,itemName)=""
 . k itemList
 . s n=0
 . s xvalue=""
 . f  s xvalue=$o(sortedList(xvalue),direction) q:xvalue=""  d
 . . s itemName=""
 . . f  s itemName=$o(sortedList(xvalue,itemName)) q:itemName=""  d
 . . . i $d(found(itemName)) q
 . . . s n=n+1
 . . . s itemList(n)=itemName
 . . . s found(itemName)=""
 ;i $g(^zewd("trace")) d trace^%zewdAPI($h_": runQuery ended")
 QUIT error
 ;
inMatchList(matchList,itemName)
 ;
 n pos,present
 ;
 s pos="",present=0
 f  s pos=$o(matchList(pos)) q:pos=""  d  q:present
 . i matchList(pos)=itemName s present=1
 QUIT present
 ;
 ;
mergeLists(toList,fromList)
 ;
 n index,itemName,maxPos,newPos,pos,posx,stop,toPos
 ;
 s pos=""
 f  s pos=$o(toList(pos)) q:pos=""  d
 . s itemName=toList(pos)
 . i itemName="" s itemName=" "
 . s index(itemName)=pos
 ;
 s pos="",maxPos=$o(toList(""),-1)
 f  s pos=$o(fromList(pos)) q:pos=""  d
 . s itemName=fromList(pos)
 . ;s toPos="",stop=0
 . ;f  s toPos=$o(toList(toPos)) q:toPos=maxPos  q:toPos=""  d  q:stop
 . ;. i toList(toPos)=itemName s stop=1
 . ;i 'stop d
 . s posx=$g(index(itemName))
 . i posx="" d
 . . s newPos=$o(toList(""),-1)+1
 . . s toList(newPos)=itemName
 . . s index(itemName)=newPos
 QUIT
 ;
queryPredicate(query,keyId,domainId,itemList,name)
 n c,compOp,error,i,inString,no,not,predicate,relation,stop,value
 ;
 i query["\'" s query=$$replaceAll^%zewdAPI(query,"\'",$c(1))
 i query["''" s query=$$replaceAll^%zewdAPI(query,"''",$c(1))
 i query[$c(2) s query=$$replaceAll^%zewdAPI(query,$c(2),$c(1))
 s error="",predicate=""
 s not=$$queryNot(.query)
 i $e(query,1)'="[" QUIT $$queryError(7)
 s inString=0
 s query=$e(query,2,$l(query))
 s stop=0
 f i=1:1:$l(query) d  q:stop
 . s c=$e(query,i)
 . i c="'" s inString='inString
 . i c="]",'inString d  q
 . . s predicate=$e(query,1,i-1)
 . . s query=$e(query,i+1,$l(query))
 . . s stop=1
 i predicate="" QUIT $$queryError(8)
 s query=$$stripSpaces(query)
 s error=$$parsePredicate(.predicate,.name,.compOp,.value)
 i error'="" QUIT error
 s no=1
 i predicate'="" d  i error'="" QUIT error
 . s name(1)=name
 . s compOp(1)=compOp
 . i value="" s value=$c(31)
 . s value(1)=value
 . f  q:predicate=""  d  q:error'=""
 . . i $e(predicate,1,3)="and" d  q
 . . . s predicate=$e(predicate,4,$l(predicate))
 . . . s error=$$parseSubPredicate(.predicate,.name,.compOp,.value,.no)
 . . . s relation(no)="&"
 . . i $e(predicate,1,2)="or" d  q
 . . . s predicate=$e(predicate,3,$l(predicate))
 . . . s error=$$parseSubPredicate(.predicate,.name,.compOp,.value,.no)
 . . . s relation(no)="!"
 d executeQuery(keyId,domainId,no,.name,.compOp,.value,.relation,.itemList)
 i not d not(.itemList)
 QUIT error
 ;
parseSubPredicate(predicate,name,compOp,value,no)
 n error
 ;
 s predicate=$$stripSpaces(predicate)
 s error=$$parsePredicate(.predicate,.name,.compOp,.value)
 i error'="" QUIT error
 i name'=name(1) QUIT $$queryError(9)
 i value="" s value=$c(31)
 s no=no+1
 s name(no)=name,compOp(no)=compOp,value(no)=value
 QUIT ""
 ;
parsePredicate(predicate,name,compOp,value)
 n error
 ;
 s predicate=$$stripSpaces(predicate)
 i $e(predicate,1)'="'" QUIT $$queryError(10)
 i $e(predicate,$l(predicate))'="'" QUIT $$queryError(11)
 s error=$$getIdentifier(.predicate,.name)
 i error'="" QUIT error
 i name="" QUIT $$queryError(13)
 s error=$$getCompOp(.predicate,.compOp)
 i error'="" QUIT error
 s error=$$getIdentifier(.predicate,.value)
 i value="" s value=$c(31)
 i error'="",compOp="starts-with",value="" s error=""
 QUIT error
 ;
getIdentifier(predicate,identifier)
 ;
 n c,error,escaped,i
 ;
 s error=""
 s identifier=""
 s escaped=0
 i $e(predicate,1)'="'" QUIT $$queryError(12)
 f i=2:1:$l(predicate) d  q:identifier'=""
 . s c=$e(predicate,i)
 . i c="\" s escaped=1 q
 . i 'escaped,c="'" d  q
 . . s identifier=$e(predicate,2,i-1)
 . . s predicate=$e(predicate,i+1,$l(predicate))
 . . s predicate=$$stripSpaces(predicate)
 . i escaped s escaped=0
 ;i identifier="" QUIT $$queryError(13)
 QUIT error
 ;
getCompOp(predicate,compOp)
 ;
 n error
 ;
 s error=""
 i $e(predicate,1)="=" d  QUIT ""
 . s compOp="="
 . s predicate=$e(predicate,2,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1,2)="<=" d  QUIT ""
 . s compOp="<="
 . s predicate=$e(predicate,3,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1,2)=">=" d  QUIT ""
 . s compOp=">="
 . s predicate=$e(predicate,3,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1,2)="!=" d  QUIT ""
 . s compOp="!="
 . s predicate=$e(predicate,3,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1)="<" d  QUIT ""
 . s compOp="<"
 . s predicate=$e(predicate,2,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1)=">" d  QUIT ""
 . s compOp=">"
 . s predicate=$e(predicate,2,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1,11)="starts-with" d  QUIT ""
 . s compOp="starts-with"
 . s predicate=$e(predicate,12,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1,4)="like" d  QUIT ""
 . s compOp="like"
 . s predicate=$e(predicate,5,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1,8)="not like" d  QUIT ""
 . s compOp="notlike"
 . s predicate=$e(predicate,9,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1,19)="does-not-start-with" d  QUIT ""
 . s compOp="does-not-start-with"
 . s predicate=$e(predicate,20,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1,6)="isNull" d  QUIT ""
 . s compOp="isNull"
 . s predicate=$e(predicate,7,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 i $e(predicate,1,9)="isNotNull" d  QUIT ""
 . s compOp="isNotNull"
 . s predicate=$e(predicate,10,$l(predicate))
 . s predicate=$$stripSpaces(predicate)
 QUIT $$queryError(14)
 ;
queryNot(query)
 n not
 s not=0
 i $e(query,1,3)="not" d
 . s not=1
 . s query=$e(query,4,$l(query))
 . s query=$$stripSpaces(query)
 QUIT not
 ;
queryError(x)
 i x=5 QUIT "InvalidSortExpression~Invalid sort expression. The sort attribute must be present in at least one of the predicates, and the predicate cannot contain the is null operator."
 QUIT "InvalidQueryExpression~The specified query expression syntax is not valid ("_x_")"
 ;
stripSpaces(string)
 QUIT $$stripSpaces^%zewdAPI(string)
 ;
not(itemList)
 ;
 n itemId,itemName,pos,selected
 ;
 s pos=""
 f  s pos=$o(itemList(pos)) q:pos=""  d
 . s itemName=itemList(pos)
 . s itemId=$$getItemId(keyId,domainId,itemName)
 . s selected(itemId)=""
 k itemList
 s itemId="",pos=0
 f  s itemId=$o(^MDB(keyId,"domains",domainId,"items",itemId)) q:itemId=""  d
 . i '$d(selected(itemId)) d
 . . s pos=pos+1
 . . s itemList(pos)=^MDB(keyId,"domains",domainId,"items",itemId)
 ;
 QUIT
 ;
executeQuery(keyId,domainId,no,name,compOp,value,relation,itemList)
 ;
 n attribId,expr,func,itemId,itemName,pos,stop,xvalue
 ;
 s attribId=""
 ;d trace^%zewdAPI("xx keyId="_keyId_"; domainId="_domainId_"; name="_name)
 i $g(name)'="" s attribId=$$getAttributeId(keyId,domainId,name)
 ;d trace^%zewdAPI("xx attribId="_attribId)
 i attribId="",compOp'="isNull" QUIT
 ;
 i 
 i value[$c(1) s value=$tr(value,$c(1),"'")
 i name[$c(1) s name=$tr(name,$c(1),"'")
 i no=1,compOp="=" d  QUIT
 . s pos=0,itemId=""
 . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,value,itemId)) q:itemId=""  d
 . . s pos=pos+1
 . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="!=" d  QUIT
 . s xvalue="",pos=0
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d
 . . i value=xvalue q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="<" d  QUIT
 . s xvalue="",stop=0,pos=0
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue'<value  q:xvalue=""  d  q:stop
 . . i $e(xvalue,1)'?1N s stop=1 q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="<=" d  QUIT
 . s xvalue="",stop=0,pos=0
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue>value  q:xvalue=""  d  q:stop
 . . i $e(xvalue,1)'?1N s stop=1 q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp=">" d  QUIT
 . s xvalue=value,stop=0,pos=0
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d  q:stop
 . . i $e(xvalue,1)'?1N s stop=1 q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp=">=" d  QUIT
 . s xvalue=value-1,stop=0,pos=0
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d  q:stop
 . . i $e(xvalue,1)'?1N s stop=1 q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="isNull" d  QUIT
 . s itemId="",pos=0
 . f  s itemId=$o(^MDB(keyId,"domains",domainId,"items",itemId)) q:itemId=""  d
 . . i attribId'="",$d(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId)) q
 . . s pos=pos+1
 . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="isNotNull" d  QUIT
 . s itemId="",pos=0
 . f  s itemId=$o(^MDB(keyId,"domains",domainId,"items",itemId)) q:itemId=""  d
 . . i '$d(^MDB(keyId,"domains",domainId,"items",itemId,"attribs",attribId)) q
 . . s pos=pos+1
 . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d
 . . i $e(xvalue,1,len)=value q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="like" d
 . i $e(value,1)="%",$e(value,$l(value))="%" d  q
 . . s value=$e(value,2,$l(value)-1)
 . . s compOp="contains"
 . i $e(value,$l(value))="%" d  q
 . . s value=$e(value,1,$l(value)-1)
 . . s compOp="starts-with"
 . i $e(value,1)="%" d  q
 . . s value=$e(value,2,$l(value))
 . . s compOp="ends-with"
 i no=1,compOp="notlike" d
 . i $e(value,1)="%",$e(value,$l(value))="%" d  q
 . . s value=$e(value,2,$l(value)-1)
 . . s compOp="does-not-contain"
 . i $e(value,$l(value))="%" d  q
 . . s value=$e(value,1,$l(value)-1)
 . . s compOp="does-not-start-with"
 . i $e(value,1)="%" d  q
 . . s value=$e(value,2,$l(value))
 . . s compOp="does-not-end-with"
 . ;s compOp="does-not-start-with"
 . ;i $e(value,$l(value))="%" s value=$e(value,1,$l(value)-1)
 i no=1,compOp="starts-with" d  QUIT
 . i value=$c(31) s value=""
 . i $l(value)=1 d
 . . s xvalue=$c($a(value)-1)
 . e  d
 . . n c
 . . q:value=""
 . . s c=$e(value,$l(value))
 . . s c=$c($a(c)-1)
 . . s xvalue=$e(value,1,$l(value)-1)_c
 . s stop=0,pos=0
 . i value?1N.N d
 . . n len
 . . s xvalue=value-1
 . . s len=$l(value)
 . . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d
 . . . i $e(xvalue,1,len)'=value q
 . . . s itemId=""
 . . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . . s pos=pos+1
 . . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 . e  d
 . . n len
 . . i value'="" d
 . . . s xvalue=xvalue_"~"
 . . . s len=$l(value)
 . . e  d
 . . . s xvalue=""
 . . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d  q:stop
 . . . i value'="",$e(xvalue,1,len)'=value s stop=1 q
 . . . s itemId=""
 . . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . . s pos=pos+1
 . . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="does-not-start-with" d  QUIT
 . n len
 . i value=$c(31) q
 . s xvalue="",len=$l(value),pos=0
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d
 . . i $e(xvalue,1,len)=value q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="contains" d  QUIT
 . n len
 . i value=$c(31) q
 . s xvalue="",pos=0
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d
 . . i xvalue'[value q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="does-not-contain" d  QUIT
 . n len
 . i value=$c(31) q
 . s xvalue="",pos=0
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d
 . . i xvalue[value q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="ends-with" d  QUIT
 . n len,xend,xlen
 . i value=$c(31) q
 . s xvalue="",len=$l(value),pos=0
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d
 . . s xlen=$l(xvalue)
 . . s xend=$e(xvalue,(xlen-len+1),xlen)
 . . i xend'=value q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1,compOp="does-not-end-with" d  QUIT
 . n len,xend,xlen
 . i value=$c(31) q
 . s xvalue="",len=$l(value),pos=0
 . f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d
 . . s xlen=$l(xvalue)
 . . s xend=$e(xvalue,(xlen-len+1),xlen)
 . . i xend=value q
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId))
 ;
 i no=1 QUIT
 s expr=""
 f i=1:1:no d
 . i i>1 s expr=expr_relation(i)
 . d
 . . i compOp(i)="=" s func="equals" q
 . . i compOp(i)="<" s func="lt" q
 . . i compOp(i)=">" s func="gt" q
 . . i compOp(i)="!=" s func="notEquals" q
 . . i compOp(i)="<=" s func="le" q
 . . i compOp(i)=">=" s func="ge" q
 . . i compOp(i)="starts-with" s func="startsWith" q
 . . i compOp(i)="notlike" d  q
 . . . i $e(value(i),1)="%",$e(value(i),$l(value(i)))="%" d  q
 . . . . s value(i)=$e(value(i),2,$l(value(i))-1)
 . . . . s func="notContains"
 . . . i $e(value(i),$l(value(i)))="%" d  q
 . . . . s value(i)=$e(value(i),1,$l(value(i))-1)
 . . . . s func="notStartsWith"
 . . . i $e(value(i),1)="%" d  q
 . . . . s value(i)=$e(value(i),2,$l(value(i)))
 . . . . s func="notEndsWith"
 . . . ;s func="notStartsWith"
 . . . ;i $e(value(i),$l(value(i)))="%" s value(i)=$e(value(i),1,$l(value(i))-1)
 . . i compOp(i)="like" d  q
 . . . i $e(value(i),1)="%",$e(value(i),$l(value(i)))="%" d  q
 . . . . s value(i)=$e(value(i),2,$l(value(i))-1)
 . . . . s func="contains"
 . . . i $e(value(i),$l(value(i)))="%" d  q
 . . . . s value(i)=$e(value(i),1,$l(value(i))-1)
 . . . . s func="startsWith"
 . . . i $e(value(i),1)="%" d  q
 . . . . s value(i)=$e(value(i),2,$l(value(i)))
 . . . . s func="endsWith"
 . . . ;s func="startsWith"
 . . . ;i $e(value(i),$l(value(i)))="%" s value(i)=$e(value(i),1,$l(value(i))-1)
 . . i compOp(i)="does-not-start-with" s func="notStartsWith"
 . s expr=expr_"$$"_func_"(xvalue,"""_value(i)_""")"
 s xvalue=""
 s attribId=$$getAttributeId(keyId,domainId,name(1))
 s pos=0
 f  s xvalue=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue)) q:xvalue=""  d
 . i @expr d
 . . s itemId=""
 . . f  s itemId=$o(^MDB(keyId,"domains",domainId,"queryIndex",attribId,xvalue,itemId)) q:itemId=""  d
 . . . s pos=pos+1
 . . . s itemList(pos)=$g(^MDB(keyId,"domains",domainId,"items",itemId)) 
 QUIT
 ;
equals(indexValue,targetValue)
 QUIT indexValue=targetValue
 ;
notEquals(indexValue,targetValue)
 QUIT indexValue'=targetValue
 ;
lt(indexValue,targetValue)
 i $e(indexValue,1)'?1N QUIT 0
 QUIT indexValue<targetValue
 ;
gt(indexValue,targetValue)
 i $e(indexValue,1)'?1N QUIT 0
 QUIT indexValue>targetValue
 ;
le(indexValue,targetValue)
 i $e(indexValue,1)'?1N QUIT 0
 QUIT indexValue'>targetValue
 ;
ge(indexValue,targetValue)
 i $e(indexValue,1)'?1N QUIT 0
 QUIT indexValue'<targetValue
 ;
startsWith(index,targetValue)
 QUIT $e(index,1,$l(targetValue))=targetValue
 ;
notStartsWith(index,targetValue)
 QUIT $e(index,1,$l(targetValue))'=targetValue
 ;
contains(indexValue,targetValue)
 QUIT indexValue[targetValue
 ;
notContains(indexValue,targetValue)
 QUIT indexValue'[targetValue
 ;
endsWith(indexValue,targetValue)
 n ilen,tlen,end
 s ilen=$l(indexValue)
 s tlen=$l(targetValue)
 s end=$e(indexValue,(ilen-tlen+1),ilen)
 QUIT end=targetValue
 ;
notEndsWith(indexValue,targetValue)
 n ilen,tlen,end
 s ilen=$l(indexValue)
 s tlen=$l(targetValue)
 s end=$e(indexValue,(ilen-tlen+1),ilen)
 QUIT end'=targetValue
 ;
hex(number)
 n hex,no,str
 s hex=""
 s str="123456789ABCDEF"
 f  d  q:number=0
 . s no=number#16
 . s number=number\16
 . i no s no=$e(str,no)
 . s hex=no_hex
 QUIT hex
 ;
hexDecode(hex)
 QUIT $f("0123456789ABCDEF",hex)-2
hexToDecimal(hex)
 ;
 n i,num
 s num=0
 f i=1:1:$l(hex) d
 . s num=num*16+$$hexDecode($e(hex,i))
 QUIT num
 ;
urlDecode(string)
 ;
 n ascii,c,hex,pos
 ;
 f  q:string'["%"  d
 . s pos=$f(string,"%")
 . s c=$e(string,pos) s c=$$zcvt^%zewdAPI(c,"l") i "0123456789abcdef"'[c s string=$$replace^%zewdAPI(string,"%",$c(1)) q
 . s c=$e(string,pos+1) s c=$$zcvt^%zewdAPI(c,"l") i "0123456789abcdef"'[c s string=$$replace^%zewdAPI(string,"%",$c(1)) q
 . s hex=$e(string,pos,pos+1)
 . i $l(hex)'=2 s string=$$replace^%zewdAPI(string,"%",$c(1)) q
 . s ascii=$$hexToDecimal(hex)
 . s string=$e(string,1,pos-2)_$c(ascii)_$e(string,pos+2,$l(string))
 i string[$c(1) s string=$$replaceAll^%zewdAPI(string,$c(1),"%")
 QUIT string
 ;
urlEscape(string)
 ;The unreserved characters are A-Z, a-z, 0-9, hyphen ( - ), underscore ( _ ), period ( . ), and tilde ( ~ ). 
 i string?1AN.AN QUIT string
 n a,c,esc,i,pass
 f i=45,46,95,126 s pass(i)=""
 s esc=""
 f i=1:1:$l(string) d
 . s c=$e(string,i)
 . i c?1AN s esc=esc_c q
 . s a=$a(c)
 . i $d(pass(a)) s esc=esc_c q
 . s a=$$hex^MDB(a)
 . s esc=esc_"%"_$$zcvt^%zewdAPI(a,"u")
 QUIT esc
 ;
installMDBX(requestId,boxUsage) ; Install M/DB:X Extensions
 n error,startTime
 s requestId=$$init(.startTime)
 s error=""
 i $t(MDBX^MDBX)'="" d
 . d install^MDBX
 e  d
 . s error="The M/DB:X routine is not available.  Download and install it and try again"
 i error'="" QUIT error
 QUIT $$end(startTime,.boxUsage)
 ;
installMDBM(requestId,boxUsage) ; Install M/DB:M(umps) Extensions
 n error,startTime
 s requestId=$$init(.startTime)
 s error=""
 i $t(MDBMumps^MDBMumps)'="" d
 . d install^MDBMumps
 e  d
 . s error="The M/DB:M routine is not available.  Download and install it and try again"
 i error'="" QUIT error
 QUIT $$end(startTime,.boxUsage)
 ;
initialise(requestId,boxUsage) ; Initialise the M/DB database
	n configFile,i,io,key,line,ok,secret,startTime,stop
	s requestId=$$init(.startTime)
	s io=$io
	s configFile="/usr/MDB/MDB.conf"
	c configFile
	o configFile:(readonly:exception="g configFileNotExists")
	u configFile
	s stop=0,key="",secret=""
	f i=1:1 r line d  q:stop
	. i $e(line,1)="#" q
	. i line["AdminstratorAccessKeyId"!(line["AdministratorAccessKeyId") d  q
	. . s key=$p(line,"=",2)
	. . s key=$$stripSpaces^%zewdAPI(key)
	. . s key=$$replaceAll^%zewdAPI(key,$c(9),"")
	. . i $e(key,1)="<" s key=""  ; user hasn't changed the explanatory text
	. i line["AdminstratorSecretKey"!(line["AdministratorSecretKey") d  q
	. . s secret=$p(line,"=",2)
	. . s secret=$$stripSpaces^%zewdAPI(secret)
	. . s secret=$$replaceAll^%zewdAPI(secret,$c(9),"")
	. . i $e(secret,1)="<" s secret=""  ; user hasn't changed the explanatory text
	. . s stop=1
	c configFile
	u io
	i key'="",secret'="" d  QUIT $$end(startTime,.boxUsage)
	. i $$createAdministrator^MDBConfig(key,secret)
	. i $$reset^MDBConfig(key,secret)
	QUIT "The contents of the Configuration File /usr/MDB/MDB.conf were invalid"
	;
configFileNotExists	
	c configFile
	u io
	QUIT "Configuration File /usr/MDB/MDB.conf was not found"
	;
initialisationResponse
	QUIT
	;
decodeBase64(string)
 ;
 n b64,context
 ;
 i $zv["GT.M" d  QUIT b64
 . s context=1
 . i $d(^zewd("config","MGWSI")) s context=0
 . s b64=$$DB64^%ZMGWSIS(string,context)
 ;
 QUIT $$b64Decode^MDBMCache(string)
 ;
encodeBase64(string)
 ;
 n b64,context
 ;
 i $zv["GT.M" d  QUIT b64
 . s context=1
 . i $d(^zewd("config","MGWSI")) s context=0
 . s b64=$$B64^%ZMGWSIS(string,context)
 ;
 QUIT $$b64Encode^MDBMCache(string)
 ;
parseSelect(selectExpression,domainName,queryExpression,attributes,orderBy,limit)
 ;select Ranking,Keyword from books where Title = 'The Right Stuff'
 ;
 n attributeList,error,no,p1
 ;
 s error=""
 s limit=""
 s selectExpression=$$convertSubstringToLowerCase(selectExpression,"select")
 i selectExpression=$$stripSpaces^%zewdAPI(selectExpression)
 i $e(selectExpression,1,7)'="select " QUIT $$invalid(1)
 s selectExpression=$$convertSubstringToLowerCase(selectExpression,"from")
 i selectExpression'[" from " QUIT $$invalid(3)
 s selectExpression=$$convertSubstringToLowerCase(selectExpression,"count(*)")
 s p1=$p(selectExpression,"select",2)
 s attributeList=$p(p1,"from",1)
 s attributeList=$$stripSpaces^%zewdAPI(attributeList)
 i attributeList="" QUIT $$invalid(2)
 i attributeList="*" d
 . ; do nothing
 e  i attributeList="count(*)" d
 . s attributes="count(*)"
 e  i attributeList'["," d
 . s attributes(1)=attributeList
 e  d
 . f no=1:1 q:attributeList=""  d
 . . s p1=$p(attributeList,",",1)
 . . s attributeList=$p(attributeList,",",2,5000)
 . . s p1=$$stripSpaces^%zewdAPI(p1)
 . . s attributes(no)=p1
 s p1=$p(selectExpression," from",2,1000)
 s domainName=p1
 s domainName=$$stripSpaces^%zewdAPI(domainName)
 s domainName=$p(domainName," ",1)
 i domainName="" QUIT $$invalid(4)
 s selectExpression=$p(selectExpression,domainName,2,5000)
 s selectExpression=$$convertSubstringToLowerCase(selectExpression,"where")
 s selectExpression=$$stripSpaces^%zewdAPI(selectExpression)
 s selectExpression=$$convertSubstringToLowerCase(selectExpression,"order by")
 s orderBy=""
 i selectExpression["order by" d
 . n attrName,dir
 . s orderBy=$p(selectExpression,"order by",2)
 . s orderBy=$$stripSpaces^%zewdAPI(orderBy)
 . s orderBy=$$convertSubstringToLowerCase(orderBy,"limit")
 . i orderBy[" limit " d
 . . s limit=$p(orderBy," limit ",2)
 . . s orderBy=$p(orderBy," limit ",1)
 . s attrName=$p(orderBy," ",1)
 . s dir=$p(orderBy," ",2)
 . i dir="" s dir="asc"
 . s orderBy="'"_attrName_"' "_dir
 . s selectExpression=$p(selectExpression,"order by",1)
 s selectExpression=$$convertSubstringToLowerCase(selectExpression,"limit")
 i limit="",selectExpression["limit " d
 . s limit=$p(selectExpression,"limit ",2)
 . s selectExpression=$p(selectExpression,"limit ",1)
 s selectExpression=$p(selectExpression,"where",2,1000)
 s queryExpression=selectExpression
 s queryExpression=$$stripSpaces^%zewdAPI(queryExpression)
 ;
 QUIT error
 ;
convertSubstringToLowerCase(string,subString)
 ;
 n lcString,newString,p1,pos,to1
 ;
 s lcString=$$zcvt^%zewdAPI(string)
 s subString=$$zcvt^%zewdAPI(subString)
 i lcString'[subString QUIT string
 s p1=$p(lcString,subString,1)
 s to1=$l(p1)
 s pos=$f(lcString,subString)
 s newString=$e(string,1,to1)_subString_$e(string,pos,$l(string))
 ;
 QUIT newString
 ;
inProc(queryExpression,expr,thisWord)
 ;
 n attrName,c,i,inValue,list,no,np,or,str,value
 ;
 i thisWord="in" s expr=expr_" "
 s list=$p(queryExpression,")",1)
 s queryExpression=$p(queryExpression,")",2,5000)
 s inValue=0,no=0,str=""
 f  q:list=""  d
 . s c=$e(list,1),list=$e(list,2,$l(list))
 . i c="'" d  q
 . . s inValue='inValue
 . . i inValue q
 . . s no=no+1
 . . s value(no)=str
 . . s str=""
 . i 'inValue q
 . s str=str_c
 s np=$l(expr," ")
 s attrName=$p(expr," ",np-2)
 s expr=$p(expr," ",1,np-3)
 s or=""
 f i=1:1:no d
 . s expr=expr_or_attrName_" = '"_value(i)_"'"
 . s or=" or "
 QUIT
 ;
executeSelect(queryExpression,itemList,keyId,itemStack)
 n c,error,expr,inAttr,lastWord,thisWord
 i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": in executeSelect.  queryExpression="_queryExpression)
 i queryExpression["''" s queryExpression=$$replaceAll^%zewdAPI(queryExpression,"''",$c(2))
 k itemList
 s error=""
 s inAttr=0,expr="",lastWord="",thisWord=""
 f  q:queryExpression=""  d  q:c=")"
 . s c=$e(queryExpression,1),queryExpression=$e(queryExpression,2,$l(queryExpression))
 . i c="(" d  q
 . . n rel
 . . i thisWord="in"!(lastWord="in") d  q
 . . . d inProc(.queryExpression,.expr,thisWord)
 . . s error=$$executeSelect(.queryExpression,.itemList,keyId,.itemStack)
 . . i error'="" s queryExpression="",expr="" q
 . . s rel=$g(itemStack("rel"))
 . . i '$d(itemStack("list")) d
 . . . m itemStack("list")=itemList
 . . e  d
 . . . i rel="or" d
 . . . . n index,itemNo,no,newList
 . . . . s no=""
 . . . . f  s no=$o(itemList(no)) q:no=""  d
 . . . . . s itemNo=itemList(no)
 . . . . . s index(itemNo)=""
 . . . . s no=""
 . . . . f  s no=$o(itemStack("list",no)) q:no=""  d
 . . . . . s itemNo=itemStack("list",no)
 . . . . . s index(itemNo)=""
 . . . . s itemNo="",no=0
 . . . . k itemStack
 . . . . f  s itemNo=$o(index(itemNo)) q:itemNo=""  d
 . . . . . s no=no+1
 . . . . . s itemStack("list",no)=itemNo
 . . . i rel="and" d
 . . . . n index,itemNo,no,newList
 . . . . s no=""
 . . . . f  s no=$o(itemList(no)) q:no=""  d
 . . . . . s itemNo=itemList(no)
 . . . . . s index(itemNo)=""
 . . . . s no=""
 . . . . f  s no=$o(itemStack("list",no)) q:no=""  d
 . . . . . s itemNo=itemStack("list",no)
 . . . . . i $d(index(itemNo)) s newList(itemNo)=""
 . . . . s itemNo="",no=0
 . . . . k itemStack
 . . . . f  s itemNo=$o(newList(itemNo)) q:itemNo=""  d
 . . . . . s no=no+1
 . . . . . s itemStack("list",no)=itemNo
 . . s queryExpression=$$stripSpaces^%zewdAPI(queryExpression)
 . . s rel=$p(queryExpression," ",1)
 . . s queryExpression=$p(queryExpression," ",2,5000)
 . . s queryExpression=$$stripSpaces^%zewdAPI(queryExpression)
 . . i queryExpression'="",$e(queryExpression,1)'="(" s queryExpression="("_queryExpression_")"
 . . s itemStack("rel")=rel
 . i c=")" d  quit  ; pop
 . . k itemList
 . i expr="",c=" " q
 . i expr="" s inAttr=1,expr="'",thisWord=expr
 . i 'inAttr d
 . . n stop
 . . s stop=0
 . . i lastWord="and",expr["between" d  q:stop
 . . . n np
 . . . s np=$l(expr," ")
 . . . i $p(expr," ",np-3)="between" d
 . . . . n p1
 . . . . s stop=1
 . . . . s p1=$p(expr," ",1,np-4)_" >= "
 . . . . s p1=p1_$p(expr," ",np-2)_" and "_$p(expr," ",np-4)_" <= "
 . . . . s expr=p1
 . . . . s lastWord="<="
 . . i lastWord="and" s inAttr=1,expr=expr_"'",thisWord="'"
 . . i lastWord="or" s inAttr=1,expr=expr_"'",thisWord="'"
 . . i lastWord="union" s inAttr=1,expr=expr_"'",thisWord="'"
 . . i lastWord="intersection" s inAttr=1,expr=expr_"'",thisWord="'"
 . i inAttr,c="=" d
 . . s c=" "
 . . i $e(queryExpression,1)'=" " s queryExpression=" "_queryExpression
 . . s queryExpression="="_queryExpression
 . i c'=" " d  q
 . . s thisWord=thisWord_c
 . . s expr=expr_c
 . i inAttr d
 . . s expr=expr_"'"
 . . s thisWord=thisWord_"'"
 . . s inAttr=0
 . s expr=expr_c
 . s lastWord=thisWord
 . s thisWord=""
 . i lastWord="is" d
 . . i $e(queryExpression,1,4)="null" d
 . . . s lastWord="isNull"
 . . . s expr=$e(expr,1,$l(expr)-3)_"isNull "
 . . . s queryExpression="'null'"_$e(queryExpression,5,$l(queryExpression))
 . . i $e(queryExpression,1,8)="not null" d
 . . . s lastWord="isNotNull"
 . . . s expr=$e(expr,1,$l(expr)-3)_"isNotNull "
 . . . s queryExpression="'null'"_$e(queryExpression,9,$l(queryExpression))
 i expr'="" d
 . n i,name,np,offset,prevName,rel,term
 . s expr=$$escVals(expr)
 . s np=$l(expr," ")
 . i np>3 d
 . . n diffNames,name
 . . f i=1:1:np s term(i)=$p(expr," ",i)
 . . s offset=4
 . . s diffNames=0
 . . s name=term(1)
 . . f i=1:offset q:'$d(term(i))  d  q:diffNames
 . . . i term(i)'=name s diffNames=1 q
 . . . i $g(term(i+3))="intersection" s diffNames=1 q
 . . . i $g(term(i+3))="union" s diffNames=1 q
 . . i diffNames d
 . . . s expr="["_term(1)_" "_term(2)_" "_term(3)
 . . . f  d  q:'$d(term(offset+1))
 . . . . s name=term(offset+1)
 . . . . s rel=term(offset)
 . . . . i rel="and"!(rel="intersection") d
 . . . . . s expr=expr_"] intersection ["
 . . . . i rel="or"!(rel="union") d
 . . . . . s expr=expr_"] union ["
 . . . . s expr=expr_term(offset+1)_" "_term(offset+2)_" "_term(offset+3)
 . . . . s offset=offset+4
 . . . s expr=expr_"]"
 . . e  d
 . . . s expr="["_expr_"]"
 . e  d
 . . s expr="["_expr_"]"
 . k itemList
 . s expr=$tr(expr,$c(1)," ")
 . i $g(orderBy)'="" s expr=expr_" sort "_orderBy
 . s error=$$runQuery(keyId,domainName,expr,,.itemList)
 QUIT error
 ;
escVals(expr)
 ;
 n c,inValue,no,str
 ;
 s inValue=0,no=0,str=""
 f  q:expr=""  d
 . s c=$e(expr,1),expr=$e(expr,2,$l(expr))
 . i c="'" d  q
 . . s inValue='inValue
 . . s str=str_c
 . i 'inValue s str=str_c q
 . i c=" " s c=$c(1)
 . s str=str_c
 QUIT str
 ;
invalid(x)
 QUIT "InvalidQueryExpression~The specified query expression syntax is not valid. ("_x_")"
 ;
runSelect(keyId,query,itemList,attributes,domainName)
 ;
 n error,limit,orderBy,queryExpression
 ;
 s error=$$parseSelect(query,.domainName,.queryExpression,.attributes,.orderBy,.limit)
 i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": completed parseSelect: queryExpression="_$g(queryExpression))
 i error'="" QUIT error
 i queryExpression="" d
 . i orderBy'="" d
 . . s error=$$invalid(5)
 . e  d
 . . s error=$$runQuery(keyId,domainName,"",,.itemList)
 e  d
 . i queryExpression["itemName()" s queryExpression=$$replaceAll^%zewdAPI(queryExpression,"itemName()","itemName{}")
 . s error=$$executeSelect(queryExpression,.itemList,keyId,.itemStack)
 . i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": finished executeSelect and returned to runSelect")
 ;***
 i $d(itemStack("list")) k itemList m itemList=itemStack("list")
 ;***
 i limit>0 d
 . n count,listCopy,no,stop
 . s count=0,no="",stop=0
 . f  s no=$o(itemList(no)) q:no=""  d  q:stop
 . . s count=count+1
 . . i count>limit s stop=1 q
 . . s listCopy(no)=itemList(no)
 . k itemList
 . m itemList=listCopy
 QUIT error
 ;
numeric(value)
 i $e(value,1)=0,$l(value)>1 QUIT 0
 i value?1N.N QUIT 1
 i value?1"-"1N.N QUIT 1
 i value?1N.N1"."1N.N QUIT 1
 i value?1"-"1N.N1"."1N.N QUIT 1
 QUIT 0
 ; 
escape(value)
 i value["&" s value=$$replaceAll^%zewdAPI(value,"&","&amp;")
 i value["<" s value=$$replaceAll^%zewdAPI(value,"<","&lt;")
 i value[">" s value=$$replaceAll^%zewdAPI(value,">","&gt;")
 QUIT value

MDBConfig^INT^1^62009,59277^0^1
MDBConfig ; M/DB: Mumps Emulation of Amazon SimpleDB - Configuration/Administration
 ;
 ; ----------------------------------------------------------------------------
 ; | M/DB                                                                     |
 ; | Copyright (c) 2004-9 M/Gateway Developments Ltd,                         |
 ; | Reigate, Surrey UK.                                                      |
 ; | All rights reserved.                                                     |
 ; |                                                                          |
 ; | http://www.mgateway.com                                                  |
 ; | Email: rtweed@mgateway.com                                               |
 ; |                                                                          |
 ; | This program is free software: you can redistribute it and/or modify     |
 ; | it under the terms of the GNU Affero General Public License as           |
 ; | published by the Free Software Foundation, either version 3 of the       |
 ; | License, or (at your option) any later version.                          |
 ; |                                                                          |
 ; | This program is distributed in the hope that it will be useful,          |
 ; | but WITHOUT ANY WARRANTY; without even the implied warranty of           |
 ; | MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            |
 ; | GNU Affero General Public License for more details.                      |
 ; |                                                                          |
 ; | You should have received a copy of the GNU Affero General Public License |
 ; | along with this program.  If not, see <http://www.gnu.org/licenses/>.    |
 ; ----------------------------------------------------------------------------
 ;
 QUIT
 ;
deleteAccount(AWSAccessKeyId,adminAWSAccessKeyId,adminAWSSecretAccessKey)
 i $g(AWSAccessKeyId)="" QUIT 0
 i $g(adminAWSAccessKeyId)="" QUIT 0
 i $g(adminAWSSecretAccessKey)="" QUIT 0
 i $g(^MDBUAF("administrator"))'=adminAWSAccessKeyId QUIT 0
 i $g(^MDBUAF("keys",adminAWSAccessKeyId,"s"))'=adminAWSSecretAccessKey QUIT 0
 i AWSAccessKeyId=adminAWSAccessKeyId QUIT 0
 k ^MDBUAF("keys",AWSAccessKeyId)
 QUIT 1
 ;
createAccount(AWSAccessKeyId,AWSSecretAccessKey,adminAWSAccessKeyId,adminAWSSecretAccessKey)
 i $g(AWSAccessKeyId)="" QUIT 0
 i $g(AWSSecretAccessKey)="" QUIT 0
 i $g(adminAWSAccessKeyId)="" QUIT 0
 i $g(adminAWSSecretAccessKey)="" QUIT 0
 i $g(^MDBUAF("administrator"))'=adminAWSAccessKeyId QUIT 0
 i $g(^MDBUAF("keys",adminAWSAccessKeyId,"s"))'=adminAWSSecretAccessKey QUIT 0
 i AWSAccessKeyId=adminAWSAccessKeyId QUIT 0
 s ^MDBUAF("keys",AWSAccessKeyId)=AWSSecretAccessKey
 QUIT 1
 ;
reset(AWSAccessKeyId,AWSSecretAccessKey)
 ;
 i $g(AWSAccessKeyId)=""  QUIT 0
 i $g(AWSSecretAccessKey)=""  QUIT 0
 i $g(^MDBUAF("administrator"))'=AWSAccessKeyId  QUIT 0
 i $g(^MDBUAF("keys",AWSAccessKeyId))'=AWSSecretAccessKey QUIT 0
 k ^MDB
 k ^MDBConfig
 k ^MDBErrors
 k ^MDBUAF
 d buildConfigFiles
 s ^MDBUAF("keys",AWSAccessKeyId)=AWSSecretAccessKey
 s ^MDBUAF("administrator")=AWSAccessKeyId
 QUIT 1
 ;
createAdministrator(AWSAccessKeyId,AWSSecretAccessKey)
 i $g(AWSAccessKeyId)="" QUIT 0
 i $g(AWSSecretAccessKey)="" QUIT 0
 i $d(^MDBUAF("administrator")) QUIT 0
 s ^MDBUAF("keys",AWSAccessKeyId)=AWSSecretAccessKey
 s ^MDBUAF("administrator")=AWSAccessKeyId
 QUIT 1
 ;
buildConfigFiles
 ;
 n code,i,line,name,np,param,params,pno,value,x
 ;
 f i=1:1 s line=$t(config+i^MDBConfig) q:line["***END***"  d
 . s line=$p(line,";;",2,200)
 . s name=$p(line,"=",1)
 . s value=$p(line,"=",2,200)
 . i name'["[" s ^MDBConfig(name)=value q
 . s params=$p(name,"[",2)
 . s name=$p(name,"[",1)
 . s params=$p(params,"]",1)
 . s x="s ^MDBConfig("""_name_""""
 . s np=$l(params,",")
 . f pno=1:1:np d
 . . s x=x_","""_$p(params,",",pno)_""""
 . s x=x_")="""_value_""""
 . x x
 f i=1:1 s line=$t(errors+i^MDBConfig) q:line["***END***"  d
 . s line=$p(line,";;",2,200)
 . s code=$p(line,";",1)
 . s value=$p(line,";",2)
 . s ^MDBErrors(code)=value
 QUIT
 ;
config
 ;;GMTOffset=+00:00
 ;;***END***
 ;
errors
 ;;InvalidParameterValue;Value ~xxx~ for parameter ~yyy~ is invalid 
 ;;MissingParameter;The request must contain the parameter ~xxx~
 ;;NumberDomainsExceeded;Number of domains limit exceeded
 ;;***END*** 

MDBMCache^INT^1^62019,43500^0^1
MDBMCache	; M/DB:Mumps - Cache-specific functions and procedures
 ;
 ; ----------------------------------------------------------------------------
 ; | M/DB                                                                     |
 ; | Copyright (c) 2004-9 M/Gateway Developments Ltd,                         |
 ; | Reigate, Surrey UK.                                                      |
 ; | All rights reserved.                                                     |
 ; |                                                                          |
 ; | http://www.mgateway.com                                                  |
 ; | Email: rtweed@mgateway.com                                               |
 ; |                                                                          |
 ; | This program is free software: you can redistribute it and/or modify     |
 ; | it under the terms of the GNU Affero General Public License as           |
 ; | published by the Free Software Foundation, either version 3 of the       |
 ; | License, or (at your option) any later version.                          |
 ; |                                                                          |
 ; | This program is distributed in the hope that it will be useful,          |
 ; | but WITHOUT ANY WARRANTY; without even the implied warranty of           |
 ; | MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            |
 ; | GNU Affero General Public License for more details.                      |
 ; |                                                                          |
 ; | You should have received a copy of the GNU Affero General Public License |
 ; | along with this program.  If not, see <http://www.gnu.org/licenses/>.    |
 ; ----------------------------------------------------------------------------
 ; 
 QUIT
 ;
baseUri()
 QUIT "/csp/ewd/mdbm.csp"
 ;
b64Encode(nextToken)
 QUIT $System.Encryption.Base64Encode($g(nextToken))
 ;
b64Decode(string)
 QUIT $System.Encryption.Base64Decode($g(string))
 ;
MD5(string)
 ;
 n hash
 ;
 s hash=$System.Encryption.MD5Hash(string)
 QUIT $System.Encryption.Base64Encode(hash)
 ;
stripSpaces(string)
 QUIT $zstrip(string,"<>W")
 ;
sign(signatureMethod,string,secretKey)
 ;
 n hash
 ;
 i $$zcvt^%zewdAPI($g(signatureMethod),"l")="hmacsha256" d
 . s hash=$System.Encryption.HMACSHA256(string,secretKey)
 e  d
 . s hash=$System.Encryption.HMACSHA1(string,secretKey)
 QUIT $System.Encryption.Base64Encode(hash) ;
 ;
csp ;
 ;
 n %CGIEVAR,%KEY,item,%zzx
 ;
 s %zzx=""
 f  s %zzx=$o(%request.Data(%zzx)) q:%zzx=""  d
 . i '$d(%request.Data(%zzx,2)) d  q
 . . s %KEY(%zzx)=%request.Data(%zzx,1)
 . s item=""
 . f  s item=$o(%request.Data(%zzx,item)) q:item=""  d
 . . s %KEY(%zzx,0,item)=%request.Data(%zzx,item)
 s %KEY("isCSP")=1
 m %CGIEVAR=%request.CgiEnvs
 k item,%zzx
 d response^MDB
 ;
 QUIT
 ;
getGlobalList(list)
 ;
 n x
 ;
 s x=""
 f  s x=$o(^$GLOBAL(x)) q:x=""  s list(x)=""
 QUIT
 ;

MDBMClient^INT^1^61536,53565^0^1
MDBMClient ; Cache Client Interface for M/DB:Mumps
 ;
access
 n ip
 s ip="192.168.1.110"
 s ip="192.168.1.107"
 s url="http://"_ip_"/mdb/request.mgwsi"
 s AWSAccessKeyId="rob"
 s AWSSecretKey="1234567"
 s GMTOffset="+00:00"
 QUIT
 ;
run(params,results)
 n AWSAccessKeyId,AWSSecretKey,GMTOffset,url
 d access
 s docName=$$request(.params,AWSAccessKeyId,AWSSecretKey,url,2)
 QUIT docName
 ;
request(params,AWSAccessKeyId,AWSSecretKey,url,signatureVersion)
 ;
 n action,boxUsage,docName,ec,em,nvpList,ok,requestId
 ;
 s signatureVersion=$g(signatureVersion) i signatureVersion="" s signatureVersion=2
 s action=$g(params("Action"))
 s docName=$$createHTTPRequest^SimpleDBClient(action,$g(AWSAccessKeyId),$g(AWSSecretKey),.params,$g(url),.em,.ec,.requestId,.boxUsage,signatureVersion,"2009-06-05")
 QUIT docName
  ;
Tests ; ****************
  ;
version
 n docName,params
 s params("Action")="GetVersion"
 s params("db")="mdbm"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
set
 n docName,params
 s params("Action")="Set"
 s params("db")="mdbm"
 s params("GlobalName")="MDBMumpsTest"
 s params("Subscript.1.Value")="a1"
 s params("Subscript.2.Value")="b1"
 s params("Subscript.3.Value")="c1"
 s params("DataValue")="hello!"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
set2
 n docName,params
 s params("Action")="Set"
 s params("db")="mdbm"
 ;s params("GlobalName")="MDBMumpsTest"
 s params("Subscript.1.Value")="a1"
 s params("Subscript.2.Value")="b1"
 s params("Subscript.3.Value")="c2"
 s params("DataValue")="world!"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
get
 n docName,params
 s params("Action")="Get"
 s params("db")="mdbm"
 s params("GlobalName")="MDBMumpsTest"
 s params("Subscript.1.Value")="a1"
 s params("Subscript.2.Value")="b1"
 s params("Subscript.3.Value")="c1"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
kill
 n docName,params
 s params("Action")="Kill"
 s params("db")="mdbm"
 s params("GlobalName")="MDBMumpsTest"
 s params("Subscript.1.Value")="a1"
 s params("Subscript.2.Value")="b1"
 s params("Subscript.3.Value")="c1"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
order
 n docName,params
 s params("Action")="Order"
 s params("db")="mdbm"
 s params("GlobalName")="MDBMumpsTest"
 s params("Subscript.1.Value")="a1"
 s params("Subscript.2.Value")="b1"
 s params("SeedValue")=""
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
order2
 n docName,params
 s params("Action")="Order"
 s params("db")="mdbm"
 s params("GlobalName")="MDBMumpsTest"
 s params("Subscript.1.Value")="a1"
 s params("Subscript.2.Value")="b1"
 s params("SeedValue")="c1"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
order3
 n docName,params
 s params("Action")="Order"
 s params("db")="mdbm"
 s params("GlobalName")="MDBMumpsTest"
 s params("Subscript.1.Value")="a1"
 s params("Subscript.2.Value")="b1"
 s params("SeedValue")="c2"
 s params("Direction")="back"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
mergeToGlobal
 n docName,params
 s params("Action")="MergeToGlobal"
 s params("db")="mdbm"
 s params("GlobalName")="MDBMumpsTest"
 s params("GlobalSubscript.1.Value")="a1"
 s params("GlobalSubscript.2.Value")="b2"
 s params("ArraySubscript.1.Name")="c4"
 s params("ArraySubscript.1.Value")="100"
 s params("ArraySubscript.2.Name")="c5"
 s params("ArraySubscript.2.Value")="200"
 s params("ArraySubscript.3.Name")="c6"
 s params("ArraySubscript.3.Value")="300"
 s params("ArraySubscript.4.Name")="c7"
 s params("ArraySubscript.4.Value")="400"
 s params("ArraySubscript.5.Name")="c7"
 s params("ArraySubscript.4.Value")="400"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
batchSet
 n docName,params
 s params("Action")="BatchSet"
 s params("db")="mdbm"
 s params("Item.1.GlobalName")="MDBMumpsTest2"
 s params("Item.1.Subscript.1.Value")="a1"
 s params("Item.1.Subscript.2.Value")="b1"
 s params("Item.1.Subscript.3.Value")="c1"
 s params("Item.1.DataValue")="hello!"
 s params("Item.2.GlobalName")="MDBMumpsTest2"
 s params("Item.2.Subscript.1.Value")="a1"
 s params("Item.2.Subscript.2.Value")="b2"
 s params("Item.2.DataValue")="world!"
 s params("Item.2.GlobalName")="MDBMumpsTest2"
 s params("Item.2.Subscript.1.Value")="a1"
 s params("Item.2.Subscript.2.Value")="b2"
 s params("Item.2.Subscript.3.Value")="c2"
 s params("Item.2.DataValue")="world 2!"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
mergeFromGlobal
 n docName,params
 s params("Action")="MergeFromGlobal"
 s params("db")="mdbm"
 s params("GlobalName")="MDBMumpsTest"
 s params("Subscript.1.Value")="a1"
 s params("Subscript.2.Value")="b1"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;
increment
 n docName,params
 s params("Action")="Increment"
 s params("db")="mdbm"
 s params("GlobalName")="MDBMumpsTest"
 s params("Subscript.1.Value")="a1"
 s params("Subscript.2.Value")="b1"
 s docName=$$run(.params)
 d outputDOM^%zewdDOM(docName,1,2)
 d removeDocument^%zewdDOM(docName)
 QUIT
 ;

MDBMgr^INT^1^62009,58967^0^1
MDBMgr ; M/DB: Mumps Emulation of Amazon SimpleDB: Manager Portal Scripts
 ;
 ; ----------------------------------------------------------------------------
 ; | M/DB                                                                     |
 ; | Copyright (c) 2004-9 M/Gateway Developments Ltd,                         |
 ; | Reigate, Surrey UK.                                                      |
 ; | All rights reserved.                                                     |
 ; |                                                                          |
 ; | http://www.mgateway.com                                                  |
 ; | Email: rtweed@mgateway.com                                               |
 ; |                                                                          |
 ; | This program is free software: you can redistribute it and/or modify     |
 ; | it under the terms of the GNU Affero General Public License as           |
 ; | published by the Free Software Foundation, either version 3 of the       |
 ; | License, or (at your option) any later version.                          |
 ; |                                                                          |
 ; | This program is distributed in the hope that it will be useful,          |
 ; | but WITHOUT ANY WARRANTY; without even the implied warranty of           |
 ; | MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            |
 ; | GNU Affero General Public License for more details.                      |
 ; |                                                                          |
 ; | You should have received a copy of the GNU Affero General Public License |
 ; | along with this program.  If not, see <http://www.gnu.org/licenses/>.    |
 ; ----------------------------------------------------------------------------
 ;
 QUIT
 ;
checkBrowser(sessid)
 ;
 n browser
 s browser=$$getServerValue^%zewdAPI("HTTP_USER_AGENT",sessid)
 s browser=$$zcvt^%zewdAPI(browser,"l")
 i browser["ipod"!(browser["ipad")!(browser["iphone") d
 . d setSessionValue^%zewdAPI("ewd_appName","imdb",sessid)
 . d setRedirect^%zewdAPI("index",sessid)
 QUIT ""
 ;
isMDBConfigured(sessid)
 n isConfigured
 s isConfigured=$d(^MDBUAF("administrator"))
 d setSessionValue^%zewdAPI("isConfigured",isConfigured,sessid)
 QUIT ""
 ;
initialise(sessid)
 ;
 i '$d(^MDBUAF("administrator")) d setRedirect^%zewdAPI("initialise",sessid) QUIT ""
 d setRedirect^%zewdAPI("login",sessid) QUIT ""
 QUIT ""
 ;
aboutAppliance(sessid)	
	;
	d setSessionValue^%zewdAPI("mdbva_version",$$version^MDB(),sessid)
	d setSessionValue^%zewdAPI("mdbva_os",$$getLinuxBuild^%zewdGTM(),sessid)
	d setSessionValue^%zewdAPI("mdbva_date",$$buildDate^MDB(),sessid)
	d setSessionValue^%zewdAPI("gtm_version",$zv,sessid)
	d setSessionValue^%zewdAPI("mgwsi_version","2.0.49",sessid)
	d setSessionValue^%zewdAPI("gtm_dateTime",$$inetDate^%zewdAPI($h),sessid)
	d setSessionValue^%zewdAPI("ewdVersion",$$version^%zewdAPI(),sessid)
	d setSessionValue^%zewdAPI("ewd_ip",$$getIP^%zewdGTM(),sessid)
	;
	QUIT ""
	;
login(sessid)
 n username,password
 s username=$$getSessionValue^%zewdAPI("username",sessid)
 s password=$$getSessionValue^%zewdAPI("password",sessid)
 i $$getSessionValue^%zewdAPI("ewd_appName",sessid)'="imdb" d
 . d deleteFromSession^%zewdAPI("username",sessid)
 . d deleteFromSession^%zewdAPI("password",sessid)
 i username="" QUIT "You must enter a username"
 i $g(^MDBUAF("administrator"))'=username QUIT "Invalid login attempt"
 i password="" QUIT "You must enter the secret key"
 i $g(^MDBUAF("keys",username))'=password QUIT "Invalid login attempt"
 QUIT ""
 ;
addUser(sessid)
 n username,password
 s username=$$getSessionValue^%zewdAPI("username",sessid)
 s password=$$getSessionValue^%zewdAPI("password",sessid)
 d deleteFromSession^%zewdAPI("username",sessid)
 d deleteFromSession^%zewdAPI("password",sessid)
 i username="" QUIT "You must enter a username"
 i password="" QUIT "You must enter the secret key"
 s ^MDBUAF("keys",username)=password
 QUIT ""
 ;
getUsers(sessid)
 n users
 m users=^MDBUAF("keys")
 d mergeArrayToSession^%zewdAPI(.users,"tmp.users",sessid)
 QUIT ""
 ;
getUserInfo(sessid)
 n secret,user
 s user=$$getRequestValue^%zewdAPI("user",sessid)
 i user="" QUIT ""
 s secret=$g(^MDBUAF("keys",user))
 d setSessionValue^%zewdAPI("tmp.secret",secret,sessid)
 QUIT ""
 ;
getUserList(sessid)
 n user
 d clearList^%zewdAPI("users",sessid)
 d appendToList^%zewdAPI("users","Select a user..",0,sessid)
 s user=""
 f  s user=$o(^MDBUAF("keys",user)) q:user=""  d
 . d appendToList^%zewdAPI("users",user,user,sessid)
 QUIT ""
 ;
getDomainList(sessid)
 n domain,domainId,user
 s user=$$getRequestValue^%zewdAPI("user",sessid)
 d setSessionValue^%zewdAPI("user",user,sessid)
 d clearList^%zewdAPI("domains",sessid)
 d appendToList^%zewdAPI("domains","Select a domain..",0,sessid)
 s domainId=""
 f  s domainId=$o(^MDB(user,"domains",domainId)) q:domainId=""  d
 . s domain=^MDB(user,"domains",domainId,"name")
 . d appendToList^%zewdAPI("domains",domain,domain,sessid)
 QUIT ""
 ;
getDomainTable(sessid)
 n attrId,attributeNames,d,no,user,domain,domainId,itemId,itemName,attribs,attrs,namex,value,nrows,valueId
 s user=$$getSessionValue^%zewdAPI("user",sessid)
 s domain=$$getRequestValue^%zewdAPI("domain",sessid)
 s domainId=$$getDomainId^MDB(user,domain)
 s namex="",no=0
 f  s namex=$o(^MDB(user,"domains",domainId,"attribsIndex",namex)) q:namex=""  d
 . s attrId=""
 . f  s attrId=$o(^MDB(user,"domains",domainId,"attribsIndex",namex,attrId)) q:attrId=""  d
 . . s no=no+1
 . . s attributeNames(no)=attrId_"^"_^MDB(user,"domains",domainId,"attribs",attrId)
 ;
 s nrows=0
 s itemId=""
 f  s itemId=$o(^MDB(user,"domains",domainId,"items",itemId)) q:itemId=""  d
 . s itemName=^MDB(user,"domains",domainId,"items",itemId)
 . s nrows=nrows+1
 . i nrows>500 q
 . s attrs(itemId,0)=itemName
 . s no=""
 . f  s no=$o(attributeNames(no)) q:no=""  d
 . . s d=attributeNames(no)
 . . s attrId=$p(d,"^",1)
 . . i '$d(^MDB(user,"domains",domainId,"items",itemId,"attribs",attrId)) d
 . . . s cell="&nbsp;"
 . . e  d
 . . . n value,valueId
 . . . s valueId="",cell=""
 . . . f  s valueId=$o(^MDB(user,"domains",domainId,"items",itemId,"attribs",attrId,"value",valueId)) q:valueId=""  d
 . . . . s value=^MDB(user,"domains",domainId,"items",itemId,"attribs",attrId,"value",valueId)
 . . . . i cell'="" s cell=cell_"<br/>"
 . . . . s cell=cell_value
 . . s attrs(itemId,no)=cell
 s attributeNames(0)="^ItemName"
 d mergeArrayToSession^%zewdAPI(.attributeNames,"attributeNames",sessid)
 d mergeArrayToSession^%zewdAPI(.attrs,"attrs",sessid)
 QUIT ""
 ;
getTimeZones(sessid)
 n i,tz
 d clearList^%zewdAPI("timezone",sessid)
 f i=11:-1:1 d
 . s tz="-"
 . i i<10 s tz=tz_0
 . s tz=tz_i_":00"
 . d appendToList^%zewdAPI("timezone",tz,tz,sessid)
 f i=0:1:12 d
 . s tz="+"
 . i i<10 s tz=tz_0
 . s tz=tz_i_":00"
 . d appendToList^%zewdAPI("timezone",tz,tz,sessid)
 s tz=$g(^MDBConfig("GMTOffset"))
 d setSessionValue^%zewdAPI("timezone",tz,sessid)
 QUIT ""
 ;
setTimeZone(sessid)
 s tz=$$getSessionValue^%zewdAPI("timezone",sessid)
 s ^MDBConfig("GMTOffset")=tz
 QUIT ""
 ;
createAttrList(listName,sessionName,sessid)
 ;
 n array,ix,name,no,list
 ;
 d mergeArrayFromSession^%zewdAPI(.array,sessionName,sessid)
 s name="",no=0
 f  s name=$o(array(name)) q:name=""  d
 . i $d(array(name))=1 d
 . . s no=no+1
 . . s list(no,"text")="Name: <span style='color:green'>"_name_"</span><br />Value: <span style='color:orange'>"_array(name)_"</span>"
 . . s list(no,"name")=name
 . . s list(no,"value")=array(name)
 . e  d
 . . s ix=""
 . . f  s ix=$o(array(name,ix)) q:ix=""  d
 . . . s no=no+1
 . . . s list(no,"text")="Name: <span style='color:green'>"_name_"</span><br />Value: <span style='color:orange'>"_array(name,ix)_"</span>"
 . . . s list(no,"name")=name
 . . . s list(no,"value")=array(name,ix)
 d mergeArrayToSession^%zewdAPI(.list,listName,sessid)
 QUIT ""
 ;

MDBMumps^INT^1^62012,48912^0^1
MDBMumps ; M/DB:Mumps API methods
 ;
 ; ----------------------------------------------------------------------------
 ; | M/DB:Mumps                                                               |
 ; | Copyright (c) 2004-9 M/Gateway Developments Ltd,                         |
 ; | Reigate, Surrey UK.                                                      |
 ; | All rights reserved.                                                     |
 ; |                                                                          |
 ; | http://www.mgateway.com                                                  |
 ; | Email: rtweed@mgateway.com                                               |
 ; |                                                                          |
 ; | This program is free software: you can redistribute it and/or modify     |
 ; | it under the terms of the GNU Affero General Public License as           |
 ; | published by the Free Software Foundation, either version 3 of the       |
 ; | License, or (at your option) any later version.                          |
 ; |                                                                          |
 ; | This program is distributed in the hope that it will be useful,          |
 ; | but WITHOUT ANY WARRANTY; without even the implied warranty of           |
 ; | MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            |
 ; | GNU Affero General Public License for more details.                      |
 ; |                                                                          |
 ; | You should have received a copy of the GNU Affero General Public License |
 ; | along with this program.  If not, see <http://www.gnu.org/licenses/>.    |
 ; ----------------------------------------------------------------------------
 ;
 QUIT
 ;
version()	
 QUIT "7"
 ;
buildDate()	
 QUIT "13 October 2010"
 ;
 ;
install
 ;k ^MDBAPI("mdbm")
 s ^MDBAPI("mdbm","BatchSet")="batchSet^MDBMumps"
 s ^MDBAPI("mdbm","Decrement")="increment^MDBMumps"
 s ^MDBAPI("mdbm","Function")="function^MDBMumps"
 s ^MDBAPI("mdbm","Get")="get^MDBMumps"
 s ^MDBAPI("mdbm","GetJSON")="getJSON^MDBMumps"
 s ^MDBAPI("mdbm","GetGlobals")="getGlobals^MDBMumps"
 s ^MDBAPI("mdbm","GetVersion")="getVersion^MDBMumps"
 s ^MDBAPI("mdbm","Increment")="increment^MDBMumps"
 s ^MDBAPI("mdbm","Kill")="kill^MDBMumps"
 s ^MDBAPI("mdbm","MergeFromGlobal")="mergeFromGlobal^MDBMumps"
 s ^MDBAPI("mdbm","MergeToGlobal")="mergeToGlobal^MDBMumps"
 s ^MDBAPI("mdbm","Order")="order^MDBMumps"
 s ^MDBAPI("mdbm","OrderAll")="orderAll^MDBMumps"
 s ^MDBAPI("mdbm","Set")="set^MDBMumps"
 s ^MDBAPI("mdbm","SetJSON")="setJSON^MDBMumps"
 s ^MDBAPI("mdbm","Transaction")="transaction^MDBMumps"
 QUIT
 ;
getVersion(%KEY,response)
 d install
 i $g(%KEY("OutputFormat"))="JSON" d  QUIT ""
 . s response(1)="{""Name"":""M/DB:Mumps"",""Build"":"_$$version()_",""Date"":"""_$$buildDate()_""",""Host"":"""_$zv_"""}"
 ;
 s response(0)="<GetVersionResult>"
 s response(1)="<Build>"_$$version()_"</Build>"
 s response(2)="<Date>"_$$buildDate()_"</Date>"
 s response(3)="</GetVersionResult>"
 QUIT ""
 ;
setJSON(%KEY,response)
 ;
 n error,globalName,props,ref,subscripts
 ;
 k response
 ;
 s error="No JSON input was supplied"
 i $g(%KEY("JSON"))'="" s error=$$parseJSON^%zewdJSON(%KEY("JSON"),.props,1)
 i error'="" s error=action_"Error~"_error q
 ;
 s subscripts=$g(%KEY("Subscripts"))
 i $e(subscripts,1)="[",$e(subscripts,$l(subscripts))="]" s subscripts=$e(subscripts,2,$l(subscripts)-1)
 ;
 i $g(%KEY("Reference"))="" QUIT "missingGlobalName~Reference (Global Name) was not specified"
 s globalName=%KEY("Reference")
 i $e(globalName,1)'="^" s globalName="^"_globalName
 i subscripts'="" s globalName=globalName_"("_subscripts_")"
 i $g(%KEY("DeleteBeforeSave"))=1 d
 . s ref="k "_globalName
 . x ref
 s ref="m "_globalName_"=props"
 x ref
 s response(1)="{""ok"":true}"
 ;
 QUIT ""
 ;
set(%KEY,response)
 ;
 n comma,error,globalName,i,name,ref,start,stop,subscript,subscripts,value
 ;
 k response
 ;
 i $g(%KEY("JSON"))'="" d
 . n name,no,props,value
 . s error=$$parseJSON^%zewdJSON(%KEY("JSON"),.props,1)
 . i error'="" s error=action_"Error~"_error q
 . s %KEY("GlobalName")=$g(props("GlobalName"))
 . s %KEY("DataValue")=$g(props("DataValue"))
 . f no=1:1 q:'$d(props("Subscripts",no))  d
 . . s value=$g(props("Subscripts",no))
 . . s %KEY("Subscript."_no_".Value")=value
 ;
 s globalName=$g(%KEY("GlobalName"))
 i globalName="" s globalName=$g(%KEY("Reference"))
 i globalName="" QUIT "missingGlobalName~GlobalName was not specified"
 s stop=0
 s name=$o(%KEY("Subscript."))
 s start=$p(name,".",2)
 i start'="",start'=1 QUIT "BadSubscriptReference~Subscript numbers must start from 1"
 s error=""
 f i=start:1 d  q:stop
 . s subscript="Subscript."_i_".Value"
 . i '$d(%KEY(subscript)) s stop=1 q
 . i %KEY(subscript)="" s error="BadSubscriptReference~Subscript values cannot be null",stop=1 q
 . s subscripts(i)=%KEY(subscript)
 i error'="" QUIT error
 s value=$g(%KEY("DataValue"))
 i value="" QUIT "missingDataValue~Data Value was not specified"
 s ref="s ^"_globalName
 i $d(subscripts) d
 . s ref=ref_"("
 . s comma=""
 . s i=""
 . f  s i=$o(subscripts(i)) q:i=""  d
 . . s ref=ref_comma_""""_subscripts(i)_""""
 . . s comma=","
 . s ref=ref_")"
 s ref=ref_"="""_value_""""
 x ref
 i $g(%KEY("OutputFormat"))="JSON" s response(1)="{""ok"":true}"
 QUIT ""
 ;
getJSON(%KEY,response)
 ;
 n arr,error,globalName,json,props,ref,subscripts
 ;
 k response
 ;
 s json=$g(%KEY("JSON"))
 s subscripts=""
 i $e(json,1)="[",$e(json,$l(json))="]" s subscripts=$e(json,2,$l(json)-1)
 ;
 i $g(%KEY("Reference"))="" QUIT "missingGlobalName~Reference (Global Name) was not specified"
 s globalName=%KEY("Reference")
 i $e(globalName,1)'="^" s globalName="^"_globalName
 s ref="m arr="_globalName
 i subscripts'="" s ref=ref_"("_subscripts_")"
 x ref
 s json=$$arrayToJSON^%zewdJSON("arr")
 s response(1)=json
 ;
 QUIT ""
 ;
get(%KEY,response)
 ;
 n comma,data,error,exists,globalName,i,name,ref,start,stop,subscript,subscripts,value
 ;
 k response
 i $g(%KEY("JSON"))'="" d
 . n json,name,no,props,value
 . s json=%KEY("JSON")
 . i $e(json,1)="[",$e(json,$l(json))="]" s %KEY("JSON")="{Subscripts:"_json_"}"
 . s error=$$parseJSON^%zewdJSON(%KEY("JSON"),.props,1)
 . i error'="" s error=action_"Error~"_error q
 . s %KEY("GlobalName")=$g(props("GlobalName"))
 . f no=1:1 q:'$d(props("Subscripts",no))  d
 . . s value=$g(props("Subscripts",no))
 . . s %KEY("Subscript."_no_".Value")=value
 ;
 s globalName=$g(%KEY("GlobalName"))
 i globalName="" s globalName=$g(%KEY("Reference"))
 i globalName="" QUIT "missingGlobalName~GlobalName was not specified"
 s stop=0
 s name=$o(%KEY("Subscript."))
 s start=$p(name,".",2)
 i start'="",start'=1 QUIT "BadSubscriptReference~Subscript numbers must start from 1"
 s error=""
 f i=start:1 d  q:stop
 . s subscript="Subscript."_i_".Value"
 . i '$d(%KEY(subscript)) s stop=1 q
 . i %KEY(subscript)="" s error="BadSubscriptReference~Subscript values cannot be null",stop=1 q
 . s subscripts(i)=%KEY(subscript)
 i error'="" QUIT error
 s ref="^"_globalName
 i $d(subscripts) d
 . s ref=ref_"("
 . s comma=""
 . s i=""
 . f  s i=$o(subscripts(i)) q:i=""  d
 . . s ref=ref_comma_""""_subscripts(i)_""""
 . . s comma=","
 . s ref=ref_")"
 s exists="s data=$d("_ref_")"
 x exists
 s ref="s value=$g("_ref_")"
 x ref
 ;
 i $g(%KEY("OutputFormat"))="JSON" d  QUIT ""
 . s response(1)="{""dataStatus"":"_data_",""value"":"""_value_"""}"
 ;
 s response(0)="<GetResult>"
 s response(1)="<DataStatus>"_data_"</DataStatus>"
 s response(2)="<DataValue>"_value_"</DataValue>"
 s response(3)="</GetResult>"
 QUIT ""
 ;
kill(%KEY,response)
 ;
 n comma,error,globalName,i,name,ref,start,stop,subscript,subscripts,value
 ;
 k response
 ;
 i $g(%KEY("JSON"))'="" d
 . n json,name,no,props,value
 . s json=%KEY("JSON")
 . i $e(json,1)="[",$e(json,$l(json))="]" s %KEY("JSON")="{Subscripts:"_json_"}"
 . s error=$$parseJSON^%zewdJSON(%KEY("JSON"),.props,1)
 . i error'="" s error=action_"Error~"_error q
 . s %KEY("GlobalName")=$g(props("GlobalName"))
 . f no=1:1 q:'$d(props("Subscripts",no))  d
 . . s value=$g(props("Subscripts",no))
 . . s %KEY("Subscript."_no_".Value")=value
 ;
 s globalName=$g(%KEY("GlobalName"))
 i globalName="" s globalName=$g(%KEY("Reference"))
 i globalName="" QUIT "missingGlobalName~GlobalName was not specified"
 s stop=0
 s name=$o(%KEY("Subscript."))
 s start=$p(name,".",2)
 i start'="",start'=1 QUIT "BadSubscriptReference~Subscript numbers must start from 1"
 s error=""
 f i=start:1 d  q:stop
 . s subscript="Subscript."_i_".Value"
 . i '$d(%KEY(subscript)) s stop=1 q
 . i %KEY(subscript)="" s error="BadSubscriptReference~Subscript values cannot be null",stop=1 q
 . s subscripts(i)=%KEY(subscript)
 i error'="" QUIT error
 s ref="k ^"_globalName
 i $d(subscripts) d
 . s ref=ref_"("
 . s comma=""
 . s i=""
 . f  s i=$o(subscripts(i)) q:i=""  d
 . . s ref=ref_comma_""""_subscripts(i)_""""
 . . s comma=","
 . s ref=ref_")"
 x ref
 i $g(%KEY("OutputFormat"))="JSON" s response(1)="{""ok"":true}"
 QUIT ""
 ;
order(%KEY,response)
 ;
 n comma,data,direction,error,globalName,i,name,next,ref,refx,seedValue,start,stop,subscript,subscripts,value
 ;
 k response
 i $g(%KEY("JSON"))'="" d
 . n json,name,no,props,value
 . s json=%KEY("JSON")
 . i $e(json,1)="[",$e(json,$l(json))="]" d
 . . s %KEY("JSON")="{Subscripts:"_json_"}"
 . s error=$$parseJSON^%zewdJSON(%KEY("JSON"),.props,1)
 . i error'="" s error=action_"Error~"_error q
 . s %KEY("GlobalName")=$g(props("GlobalName"))
 . f no=1:1 q:'$d(props("Subscripts",no))  d
 . . s value=$g(props("Subscripts",no))
 . . s %KEY("Subscript."_no_".Value")=value
 . s %KEY("SeedValue")=value
 . k %KEY("Subscript."_(no-1)_".Value")
 ;
 s globalName=$g(%KEY("GlobalName"))
 i globalName="" s globalName=$g(%KEY("Reference"))
 i globalName="" QUIT "missingGlobalName~GlobalName was not specified"
 s direction=$g(%KEY("Direction"))
 i direction="" s direction="forwards"
 s direction=$$zcvt^%zewdAPI(direction,"l")
 s stop=0
 s name=$o(%KEY("Subscript."))
 s start=$p(name,".",2)
 i start'="",start'=1 QUIT "BadSubscriptReference~Subscript numbers must start from 1"
 s error=""
 f i=start:1 d  q:stop
 . s subscript="Subscript."_i_".Value"
 . i '$d(%KEY(subscript)) s stop=1 q
 . i %KEY(subscript)="" s error="BadSubscriptReference~Subscript values cannot be null",stop=1 q
 . s subscripts(i)=%KEY(subscript)
 i error'="" QUIT error
 ;i '$d(subscripts) QUIT "MissingSubscripts~No subscripts were specified"
 i '$d(%KEY("SeedValue")) QUIT "missingSeedValue~Seed Value was not specified"
 s seedValue=$g(%KEY("SeedValue"))
 s ref="^"_globalName_"("
 i $d(subscripts) d
 . s comma=""
 . s i=""
 . f  s i=$o(subscripts(i)) q:i=""  d
 . . s ref=ref_comma_""""_subscripts(i)_""""
 . . s comma=","
 . s ref=ref_comma
 s refx=ref
 s ref=ref_""""_seedValue_""")"
 s ref="s next=$o("_ref
 i direction="backwards"!(direction="back")!(direction<0) s ref=ref_",-1"
 s ref=ref_")"
 x ref
 ;
 i next="" d
 . s data=0
 . s value=""
 e  d
 . s refx=refx_""""_next_""")"
 . s ref="s data=$d("_refx_")"
 . x ref
 . s ref="s value=$g("_refx_")"
 . x ref
 ;
 i $g(%KEY("OutputFormat"))="JSON" s response(1)="{""subscriptValue"":"""_next_""",""dataStatus"":"""_data_""",""dataValue"":"""_value_"""}" QUIT ""
 s response(0)="<OrderResult>"
 s response(1)="<NextValue>"_next_"</NextValue>"
 s response(2)="</OrderResult>"
 QUIT ""
 ;
orderAll(%KEY,response)
 ;
 n comma,data,direction,error,globalName,i,name,next,ref,refx,seedValue,start,stop,subscript,subscripts,value
 ;
 k response
 i $g(%KEY("JSON"))'="" d
 . n json,name,no,props,value
 . s json=%KEY("JSON")
 . i $e(json,1)="[",$e(json,$l(json))="]" d
 . . s %KEY("JSON")="{Subscripts:"_json_"}"
 . s error=$$parseJSON^%zewdJSON(%KEY("JSON"),.props,1)
 . i error'="" s error=action_"Error~"_error q
 . s %KEY("GlobalName")=$g(props("GlobalName"))
 . f no=1:1 q:'$d(props("Subscripts",no))  d
 . . s value=$g(props("Subscripts",no))
 . . s %KEY("Subscript."_no_".Value")=value
 . s %KEY("SeedValue")=""
 ;
 s globalName=$g(%KEY("GlobalName"))
 i globalName="" s globalName=$g(%KEY("Reference"))
 i globalName="" QUIT "missingGlobalName~GlobalName was not specified"
 s stop=0
 s name=$o(%KEY("Subscript."))
 s start=$p(name,".",2)
 i start'="",start'=1 QUIT "BadSubscriptReference~Subscript numbers must start from 1"
 s error=""
 f i=start:1 d  q:stop
 . s subscript="Subscript."_i_".Value"
 . i '$d(%KEY(subscript)) s stop=1 q
 . i %KEY(subscript)="" s error="BadSubscriptReference~Subscript values cannot be null",stop=1 q
 . s subscripts(i)=%KEY(subscript)
 i error'="" QUIT error
 ;i '$d(subscripts) QUIT "MissingSubscripts~No subscripts were specified"
 i '$d(%KEY("SeedValue")) QUIT "missingSeedValue~Seed Value was not specified"
 s seedValue=$g(%KEY("SeedValue"))
 s ref="^"_globalName_"("
 i $d(subscripts) d
 . s comma=""
 . s i=""
 . f  s i=$o(subscripts(i)) q:i=""  d
 . . s ref=ref_comma_""""_subscripts(i)_""""
 . . s comma=","
 . s ref=ref_comma
 s refx=ref
 s ref=ref_""""_seedValue_""")"
 s ref="s next=$o("_ref_")"
 x ref
 s next(1)=next
 f i=2:1 s next=$o(^(next)) q:next=""  s next(i)=next
 ;
 i '$d(next(2)) d
 . s json="[]"
 e  d
 . s json="[",comma=""
 . f i=1:1 q:'$d(next(i))  s json=json_comma_""""_next(i)_"""",comma=","
 . s json=json_"]"
 ;
 i $g(%KEY("OutputFormat"))="JSON" s response(1)=json QUIT ""
 QUIT ""
 ;
mergeToGlobal(%KEY,response)
 ;
 n comma,error,globalName,i,name,ref,start,stop,subscript,subscriptName,subscripts,subscriptValue,value
 ;
 k response
 s globalName=$g(%KEY("GlobalName"))
 i globalName="" QUIT "missingGlobalName~GlobalName was not specified"
 s stop=0
 s name=$o(%KEY("GlobalSubscript."))
 s start=$p(name,".",2)
 i start'="",start'=1 QUIT "BadGlobalSubscriptReference~Global Subscript numbers must start from 1"
 s error=""
 f i=start:1 d  q:stop
 . s subscript="GlobalSubscript."_i_".Value"
 . i '$d(%KEY(subscript)) s stop=1 q
 . i %KEY(subscript)="" s error="BadGlobalSubscriptReference~Global Subscript values cannot be null",stop=1 q
 . s subscripts(i)=%KEY(subscript)
 i error'="" QUIT error
 ;
 s stop=0
 k ^CacheTempEWD($j)
 s name=$o(%KEY("ArraySubscript."))
 s start=$p(name,".",2)
 s error=""
 f i=start:1 d  q:stop
 . s subscriptName="ArraySubscript."_i_".Name"
 . s subscriptValue="ArraySubscript."_i_".Value"
 . i '$d(%KEY(subscriptName)) s stop=1 q
 . i '$d(%KEY(subscriptValue)) s stop=1,error="MissingArrayValue~Value not specified for Array Subscript "_i q
 . i %KEY(subscriptName)="" s error="BadArraySubscriptReference~Array Subscript values cannot be null",stop=1 q
 . s ^CacheTempEWD($j,%KEY(subscriptName))=%KEY(subscriptValue)
 i error'="" QUIT error
 s ref="m ^"_globalName
 i $d(subscripts) d
 . s ref=ref_"("
 . s comma=""
 . s i=""
 . f  s i=$o(subscripts(i)) q:i=""  d
 . . s ref=ref_comma_""""_subscripts(i)_""""
 . . s comma=","
 . s ref=ref_")"
 s ref=ref_"=^CacheTempEWD($j)"
 x ref
 k ^CacheTempEWD($j)
 QUIT ""
 ;
batchSet(%KEY,response)
 ;
 n comma,error,globalName,i,name,ref,start,start2,stop,stop2,subscript,subscripts,value
 ;
 k response
 s stop=0
 s name=$o(%KEY("Item."))
 s start=$p(name,".",2)
 s error=""
 f i=start:1 d  q:stop
 . i $g(^MDBMumps("stop"))=1 s stop=1 k ^MDBMumps("stop") q
 . k subscripts
 . s ref="Item."_i_".GlobalName"
 . s globalName=$g(%KEY(ref))
 . i globalName="" d  s stop=1 q
 . . i i=1 s error="MissingGlobalName~Item 1: Global Name missing"
 . s stop2=0
 . s name=$o(%KEY("Item."_i_".Subscript."))
 . s start2=$p(name,".",4)
 . i start2'="",start2'=1 s error="BadSubscriptReference~Item "_i_": Subscript numbers must start from 1",stop=1 q
 . f j=start2:1 d  q:stop2
 . . s subscript="Item."_i_".Subscript."_j_".Value"
 . . i '$d(%KEY(subscript)) s stop2=1 q
 . . i %KEY(subscript)="" s error="BadSubscriptReference~Item "_i_": Subscript values cannot be null",stop=1 q
 . . s subscripts(j)=%KEY(subscript)
 . i error'="" s stop=1 q
 . s value=$g(%KEY("Item."_i_".DataValue"))
 . i value="" QUIT "missingDataValue~Item "_i_": Data Value was not specified"
 . s ref="s ^"_globalName
 . i $d(subscripts) d
 . . s ref=ref_"("
 . . s comma=""
 . . s j=""
 . . f  s j=$o(subscripts(j)) q:j=""  d
 . . . s ref=ref_comma_""""_subscripts(j)_""""
 . . . s comma=","
 . . s ref=ref_")"
 . s ref=ref_"="""_value_""""
 . x ref
 QUIT error
 ;
parseSubscripts(string,subs)
 n c,inquotes,len,nsub,stop,str
 ;
 s len=$l(string)
 s inquotes=0,str="",nsub=0,stop=0
 k subs
 f i=1:1:len d  q:stop
 . s c=$e(string,i)
 . i c="""" d  q
 . . i $e(string,i+1)="""" s str=str_"""""",i=i+1 q
 . . s inquotes='inquotes
 . i c=",",'inquotes d  q
 . . s nsub=nsub+1
 . . s subs(nsub)=str
 . . s str=""
 . i c=")" i 'inquotes s stop=1 q
 . s str=str_c
 i str'="" s subs(nsub+1)=str
 QUIT
 ;
mergeFromGlobal(%KEY,response)
 ;
 n comma,data,error,exists,globalName,i,lineNo,name,recNo,ref,start,stop
 n string,subs,subscript,subscripts,value
 ;
 k response
 s globalName=$g(%KEY("GlobalName"))
 i globalName="" QUIT "missingGlobalName~GlobalName was not specified"
 s stop=0
 s name=$o(%KEY("Subscript."))
 s start=$p(name,".",2)
 i start'="",start'=1 QUIT "BadSubscriptReference~Subscript numbers must start from 1"
 s error=""
 f i=start:1 d  q:stop
 . s subscript="Subscript."_i_".Value"
 . i '$d(%KEY(subscript)) s stop=1 q
 . i %KEY(subscript)="" s error="BadSubscriptReference~Subscript values cannot be null",stop=1 q
 . s subscripts(i)=%KEY(subscript)
 i error'="" QUIT error
 s ref="^"_globalName
 i $d(subscripts) d
 . s ref=ref_"("
 . s comma=""
 . s i=""
 . f  s i=$o(subscripts(i)) q:i=""  d
 . . s ref=ref_comma_""""_subscripts(i)_""""
 . . s comma=","
 . s ref=ref_")"
 k array
 s ref="m array="_ref
 x ref
 i '$d(array) s response(0)="<MergeFromGlobalResult />" QUIT
 s response(0)="<MergeFromGlobalResult>",lineNo=1
 s recNo=0
 i $d(array)=11 d
 . s response(lineNo)="<Record>",lineNo=lineNo+1
 . s response(lineNo)="<DataValue>"_array_"</DataValue>",lineNo=lineNo+1
 . s response(lineNo)="</Record>",lineNo=lineNo+1
 s ref="array("""")"
 s stop=0
 f  d  q:stop
 . s ref=$q(@ref)
 . i ref="" s stop=1 q
 . s recNo=recNo+1
 . s response(lineNo)="<Record>",lineNo=lineNo+1
 . s string=$p(ref,"(",2,5000)
 . k subs
 . d parseSubscripts(string,.subs)
 . s i=""
 . f  s i=$o(subs(i)) q:i=""  d
 . . s response(lineNo)="<Subscript no='"_i_"'>"_subs(i)_"</Subscript>",lineNo=lineNo+1
 . s data=@ref
 . s response(lineNo)="<DataValue>"_data_"</DataValue>",lineNo=lineNo+1
 . s response(lineNo)="</Record>",lineNo=lineNo+1
 s response(lineNo)="</MergeFromGlobalResult>"
 QUIT ""
 ;
increment(%KEY,response)
 ;
 n comma,delta,error,globalName,i,name,next,ref,start,stop,subscript,subscripts
 ;
 k response
 i $g(%KEY("JSON"))'="" d
 . n json,name,no,props,value
 . s json=%KEY("JSON")
 . i $e(json,1)="[",$e(json,$l(json))="]" s %KEY("JSON")="{Subscripts:"_json_"}"
 . s error=$$parseJSON^%zewdJSON(%KEY("JSON"),.props,1)
 . i error'="" s error=action_"Error~"_error q
 . s %KEY("GlobalName")=$g(props("GlobalName"))
 . f no=1:1 q:'$d(props("Subscripts",no))  d
 . . s value=$g(props("Subscripts",no))
 . . s %KEY("Subscript."_no_".Value")=value
 ;
 s globalName=$g(%KEY("GlobalName"))
 i globalName="" s globalName=$g(%KEY("Reference"))
 i globalName="" QUIT "missingGlobalName~GlobalName was not specified"
 s stop=0
 s name=$o(%KEY("Subscript."))
 s start=$p(name,".",2)
 i start'="",start'=1 QUIT "BadSubscriptReference~Subscript numbers must start from 1"
 s error=""
 f i=start:1 d  q:stop
 . s subscript="Subscript."_i_".Value"
 . i '$d(%KEY(subscript)) s stop=1 q
 . i %KEY(subscript)="" s error="BadSubscriptReference~Subscript values cannot be null",stop=1 q
 . s subscripts(i)=%KEY(subscript)
 i error'="" QUIT error
 s ref="^"_globalName
 i $d(subscripts) d
 . s ref=ref_"("
 . s comma=""
 . s i=""
 . f  s i=$o(subscripts(i)) q:i=""  d
 . . s ref=ref_comma_""""_subscripts(i)_""""
 . . s comma=","
 . s ref=ref_")"
 s delta=$g(%KEY("Delta")) i delta="" s delta=1
 i $g(%KEY("Action"))="Decrement" s delta=-delta
 s ref="s next=$increment("_ref_","_delta_")"
 x ref
 i $g(%KEY("OutputFormat"))="JSON" s response(1)="{""value"":"_next_"}" QUIT ""
 s response(0)="<IncrementResult>"
 s response(1)="<NextValue>"_next_"</NextValue>"
 s response(2)="</IncrementResult>"
 QUIT ""
 ;
function(%KEY,response)
 ;
 n func,functionName,json,paramList,result
 ;
 k response
 s json=$g(%KEY("JSON"))
 s paramList=""
 i $e(json,1)="[",$e(json,$l(json))="]" s paramList=$e(json,2,$l(json)-1)
 ;
 i $g(%KEY("Reference"))="" QUIT "missingFunctionName~Reference (Function Name) was not specified"
 s functionName=%KEY("Reference")
 i $e(functionName,1,2)="##"!($e(functionName,1,2)="$$") d
 . s func=functionName_"("
 e  d
 . s func="$$"_functionName_"("
 s func="s result="_func_paramList_")"
 d
 . n (func,result)
 . x func
 s response(1)="{""value"":"""_result_"""}"
 ;
 QUIT ""
 ;
transaction(%KEY,response)
 ;
 n error,globalName,i,json,props,ref,result,stop,subscripts
 ;
 k response
 i $g(%KEY("JSON"))="" QUIT "missingJSON~JSON transaction document was not specified"
 s json=%KEY("JSON")
 s error=$$parseJSON^%zewdJSON(json,.props,1)
 ;
 s stop=0,error=""
 f i=1:1 q:'$d(props(i))  d  q:stop
 . s method=$g(props(i,"method"))
 . i method="" s stop=1,error="Missing method in JSON transaction document at step "_i q
 . i method="setJSON" d  q:stop
 . . n json
 . . m json=props(i,"json")
 . . i '$d(json) s stop=1,error="Missing JSON document in JSON transaction document at step "_i q
 . . s globalName=$g(props(i,"globalName"))
 . . i globalName="" s stop=1,error="Missing Global name in JSON transaction document at step "_i q
 . . i $e(globalName,1)'="^" s globalName="^"_globalName
 . . s ref="m "_globalName
 . . i $d(props(i,"subscripts")) d
 . . . n comma,j
 . . . s ref=ref_"(",comma=""
 . . . f j=1:1 q:'$d(props(i,"subscripts",j))  d
 . . . . s ref=ref_comma_""""_props(i,"subscripts",j)_"""",comma=","
 . . . s ref=ref_")"
 . . s ref=ref_"=json"
 . . x ref
 . i method="kill" d  q:stop
 . . s globalName=$g(props(i,"globalName"))
 . . i globalName="" s stop=1,error="Missing Global name in JSON transaction document at step "_i q
 . . i $e(globalName,1)'="^" s globalName="^"_globalName
 . . s ref="k "_globalName
 . . i $d(props(i,"subscripts")) d
 . . . n comma,j
 . . . s ref=ref_"(",comma=""
 . . . f j=1:1 q:'$d(props(i,"subscripts",j))  d
 . . . . s ref=ref_comma_""""_props(i,"subscripts",j)_"""",comma=","
 . . . s ref=ref_")"
 . . x ref
 ;
 i stop QUIT "transactionError~"_error
 s response(1)="{""ok"":true}"
 QUIT ""
 ;
getGlobals(%KEY,response)
 i $g(%KEY("OutputFormat"))="JSON" d  QUIT ""
 . n comma,list,x
 . i $zv["GT.M" d
 . . s x="^%"
 . . i $d(@x) s list(x)=""
 . . f  s x=$order(@x) q:x=""  s list(x)=""
 . . ;
 . e  d
 . . d getGlobalList^MDBMCache(.list)
 . s response(1)="["
 . s x="",comma=""
 . f  s x=$o(list(x)) q:x=""  d
 . . s response(1)=response(1)_comma_""""_$e(x,2,$l(x))_""""
 . . s comma=","
 . s response(1)=response(1)_"]"
 ;
 QUIT ""
 ;

zmwire^INT^1^62019,43630^0^1
zmwire ; M/Wire Protocol for M Systems (eg GT.M, Cache)
 ;
 ; ----------------------------------------------------------------------------
 ; | M/Wire                                                                   |
 ; | Copyright (c) 2004-9 M/Gateway Developments Ltd,                         |
 ; | Reigate, Surrey UK.                                                      |
 ; | All rights reserved.                                                     |
 ; |                                                                          |
 ; | http://www.mgateway.com                                                  |
 ; | Email: rtweed@mgateway.com                                               |
 ; |                                                                          |
 ; | This program is free software: you can redistribute it and/or modify     |
 ; | it under the terms of the GNU Affero General Public License as           |
 ; | published by the Free Software Foundation, either version 3 of the       |
 ; | License, or (at your option) any later version.                          |
 ; |                                                                          |
 ; | This program is distributed in the hope that it will be useful,          |
 ; | but WITHOUT ANY WARRANTY; without even the implied warranty of           |
 ; | MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            |
 ; | GNU Affero General Public License for more details.                      |
 ; |                                                                          |
 ; | You should have received a copy of the GNU Affero General Public License |
 ; | along with this program.  If not, see <http://www.gnu.org/licenses/>.    |
 ; ----------------------------------------------------------------------------
 ;
 QUIT
 ;
 ; By default this server code runs on port 6330
 ;
 ; For GT.M systems it is invoked via xinetd:
 ;   Edit /etc/services and add the line:
 ; 
 ;    mwire  6330/tcp  # Service for M/Wire Protocol
 ;
 ;   Copy the file mwire to /etc/xinetd.d/mwire
 ;   Copy the file zmwire to /usr/local/gtm/zmwire and change 
 ;    its permissions to executable (eg 755)
 ;
 ;   These files may be edited to change the paths or port number
 ;   Restart xinetd using: sudo /etc/init.d/xinetd restart
 ;
 ;   On GT.M systems you must also have installed MGWSI or m_apache
 ;     in order to provide the MD5 hashing function for passwords
 ;     Alternatively substitute the MD5 callout to the MD5 function of your choice
 ;
 ; For Cache systems, it is invoked via the M/Wire Daemon routine
 ;   which should be running as a jobbed process:
 ;
 ;     job start^zmwireDaemon
 ;
 ;   You can change the port number by simply editing the line
 ; 
 ;      port+1^zmwireDaemon
 ;
 ;    Stop the Daemon process using ^RESJOB and restart it.
 ;
mwireVersion
 ;;Build 6 Beta
 ;
mwireDate
 ;;15 October 2010
 ;
version
 ;
 w "+M/Wire "_$p($t(mwireVersion+1),";;",2,2000)_$c(13,10)
 QUIT
 ;
build
 ;
 n response
 ;
 s response="*3"_$c(13,10)
 w response
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("build: "_response_" sent")
 ;
 s response=$p($t(mwireVersion+1),";;",2,2000)
 s response="$"_$l(response)_$c(13,10)_response_$c(13,10)
 w response
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("build: "_response_" sent")
 ;
 s response=$p($t(mwireDate+1),";;",2,2000)
 s response="$"_$l(response)_$c(13,10)_response_$c(13,10)
 w response
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("build: "_response_" sent")
 ;
 s response=$zv
 s response="$"_$l(response)_$c(13,10)_response_$c(13,10)
 w response
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("build: "_response_" sent")
 QUIT
 ;
command ;
 n authNeeded,c,crlf,input
 ;
 d cleardown
 i $zv["GT.M" s $zint="d monitoroutput"
 s ^zmwire("connected",$j)=""
 l +^zmwire("connected",$j)
 s crlf=$c(13,10)
 s authNeeded=0
 s role="user"
 i $d(^zmwire("auth")) s authNeeded=1
loop
 r *c 
 i $c(c)="*" d
 . s input=$$multiBulkRequest()
 . d log(input)
 . i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": mb mwire input: "_input)
 e  d
 . r input s input=$c(c)_input 
 . d log(input)
 . i $g(^zewd("trace"))=1 d trace^%zewdAPI($h_": mwire input: "_input)
 . i input'="" d
 . . i $zv["GT.M" s input=$e(input,1,$l(input)-2)
 . . i $zv["Cache" s input=$e(input,1,$l(input)-1)
 i input="" g loop
 i input="EXIT" g halt
 i input="QUIT" g quit
 i input="HALT" g halt
 ;
 i authNeeded,$e(input,1,4)'="AUTH" w "-Authentication required"_$c(13,10) g loop
 i 'authNeeded,$e(input,1,4)="AUTH" w "-Authentication ignored"_$c(13,10) g loop
 i $e(input,1,5)="AUTH " d auth($e(input,6,$l(input))) g loop
 i 'authNeeded!(role="admin"),$e(input,1,12)="SETPASSWORD " d setpassword($e(input,13,$l(input))) g loop
 ;
 i $e(input,1,8)="GETBUILD" d build g loop
 i $e(input,1,4)="SET " d set($e(input,5,$l(input))) g loop
 i $e(input,1,4)="GET " d get($e(input,5,$l(input))) g loop
 i $e(input,1,5)="KILL " d kill($e(input,6,$l(input))) g loop
 i $e(input,1,4)="DEL " d kill($e(input,5,$l(input))) g loop
 i $e(input,1,5)="DATA " d data($e(input,6,$l(input))) g loop
 i $e(input,1,7)="EXISTS " d data($e(input,8,$l(input))) g loop
 i $e(input,1,5)="INCR " d incr($e(input,6,$l(input))) g loop
 i $e(input,1,7)="INCRBY " d incrby($e(input,8,$l(input))) g loop
 i $e(input,1,5)="DECR " d decr($e(input,6,$l(input))) g loop
 i $e(input,1,7)="DECRBY " d decrby($e(input,8,$l(input))) g loop
 i $e(input,1,5)="LOCK " d lock($e(input,6,$l(input))) g loop
 i $e(input,1,7)="UNLOCK " d unlock($e(input,8,$l(input))) g loop
 i $e(input,1,6)="ORDER " d order($e(input,7,$l(input))) g loop
 i $e(input,1,5)="NEXT " d order($e(input,6,$l(input))) g loop
 i $e(input,1,14)="NEXTSUBSCRIPT " d nextSubscript($e(input,15,$l(input)),1) g loop
 i $e(input,1,18)="PREVIOUSSUBSCRIPT " d nextSubscript($e(input,19,$l(input)),-1) g loop
 i $e(input,1,9)="ORDERALL " d orderall($e(input,10,$l(input))) g loop
 i $e(input,1,10)="GETGLOBAL " d getGlobal($e(input,11,$l(input))) g loop
 i $e(input,1,11)="GETGLOBALS2" d getGlobals() g loop
 i $e(input,1,10)="GETGLOBALS" d getGlobalList() g loop
 i $e(input,1,11)="GETALLSUBS " d orderall($e(input,12,$l(input))) g loop
 i $e(input,1,14)="GETSUBSCRIPTS " d getAllSubscripts($e(input,15,$l(input))) g loop
 i $e(input,1,9)="REVORDER " d reverseorder($e(input,10,$l(input))) g loop
 i $e(input,1,9)="PREVIOUS " d reverseorder($e(input,10,$l(input))) g loop
 i $e(input,1,6)="QUERY " d query($e(input,7,$l(input))) g loop
 i $e(input,1,9)="QUERYGET " d queryget($e(input,10,$l(input))) g loop
 i $e(input,1,14)="GETJSONSTRING " d getJSON($e(input,15,$l(input))) g loop
 i $e(input,1,14)="SETJSONSTRING " d setJSON($e(input,15,$l(input))) g loop
 i $e(input,1,10)="MERGEFROM " d mergefrom($e(input,11,$l(input))) g loop
 i $e(input,1,11)="GETSUBTREE " d mergefrom($e(input,12,$l(input))) g loop
 i $e(input,1,8)="MERGETO " d mergeto($e(input,9,$l(input))) g loop
 i $e(input,1,11)="SETSUBTREE " d mergeto($e(input,12,$l(input))) g loop
 i $e(input,1,9)="FUNCTION " d function($e(input,10,$l(input))) g loop
 i $e(input,1,8)="EXECUTE " d function($e(input,9,$l(input))) g loop
 i $e(input,1,6)="TSTART" d tstart g loop
 i $e(input,1,7)="TCOMMIT" d tcommit g loop
 i $e(input,1,9)="TROLLBACK" d trollback g loop
 i $e(input,1,5)="MDATE" d mdate g loop
 i $e(input,1,9)="PROCESSID" d processid g loop
 i $e(input,1,7)="VERSION" d version g loop
 i $e(input,1,8)="MVERSION" d zv g loop
 i $e(input,1,4)="PING" d ping g loop
 i $e(input,1,4)="INFO" d info g loop
 i $e(input,1,7)="MONITOR" d monitor g loop
 w "-"_input_" not recognized",crlf
 g loop
 ;
multiBulkRequest()
 ;
 n buff,c,command,i,input,j,len,noOfCommands,param,space,stop
 ;
 s stop=0,buff=""
 f  q:stop  d
 . r *c i c=13 s stop=1 q
 . s buff=buff_$c(c)
 ;
 s noOfCommands=buff
 s buff=""
 r *c i c'=10 s buff=buff_$c(c)
 f i=1:1:noOfCommands d
 . s stop=0
 . f  q:stop  d
 . . r *c
 . . i $c(c)="$",buff="" q
 . . i c=13 s stop=1 q
 . . s buff=buff_$c(c)
 . s len=buff
 . ;
 . s buff=""
 . r *c i c'=10 s buff=buff_$c(c)
 . f j=1:1:len r *c s buff=buff_$c(c)
 . s param(i)=buff
 . s buff=""
 . r *c r *c ; terminating CRLF
 s space="",input=""
 s param(1)=$$zcvt^%zewdAPI(param(1),"U")
 i param(1)="SET" d  QUIT input
 . s input=param(1)_" "_param(2)_" "_$l(param(3))_$c(13,10)_param(3)
 i param(1)="SETJSONSTRING" d  QUIT input
 . s input=param(1)_" "_param(2)_$c(13,10)_param(3)_$c(13,10)_param(4)
 i param(1)="EXECUTE" d  QUIT input
 . i $e(param(3),1)="[" d
 . . s input=param(1)_" "_param(2)_"("_$e(param(3),2,$l(param(3))-1)_")"
 . e  d
 . . s input=param(1)_" "_param(2)
 ;
 f i=1:1:noOfCommands d
 . s input=input_space_param(i)
 . s space=" " 
 ;
 QUIT input
 ;
halt
 k ^zmwire("connected",$j)
 HALT
 ;
quit
 ;
 i '$d(^zmwire("monitor","listener",$j)) g halt
 k ^zmwire("monitor","listener",$j)
 g loop
 ;
cleardown
 ;
 n ignore,pid
 ;
 s pid=""
 f  s pid=$o(^zmwire("connected",pid)) q:pid=""  d
 . i pid=$j q
 . s ignore=1
 . l +^zmwire("connected",pid):0 e  s ignore=0
 . i ignore d
 . . l -^zmwire("connected",pid)
 . . k ^zmwire("connected",pid)
 . . k ^zmwire("monitor","listener",pid)
 . . k ^zmwire("monitor","output",pid)
 s pid=""
 f  s pid=$o(^zmwire("monitor","output",pid)) q:pid=""  d
 . i pid=$j q
 . s ignore=1
 . l +^zmwire("connected",pid):0 e  s ignore=0
 . l -^zmwire("connected",pid)
 . i ignore d
 . . k ^zmwire("monitor","output",pid)
 QUIT
 ;
monitor
 ;
 i $zv'["GT.M" w "-Command unavailable"_$c(13,10) QUIT
 n quit
 ;
 s ^zmwire("monitor","listener",$j)=""
 w "+OK"_$c(13,10)
 f  h 1 r quit:0  i $e(quit,1,4)="QUIT" q
 k ^zmwire("monitor","listener",$j)
 w "+OK"_$c(13,10)
 QUIT
 ;
log(input)
 ;
 i $zv'["GT.M" QUIT
 ;
 n dev,inputr,io,lineNo,pid
 ;
 i input["AUTH" QUIT
 i input["QUIT" QUIT
 i input["EXIT" QUIT
 i input["HALT" QUIT
 s inputr=$re(input)
 i $e(inputr,1,2)'=$c(10,13) s input=input_$c(13,10)
 s pid=""
 f  s pid=$o(^zmwire("monitor","listener",pid)) q:pid=""  d
 . i pid=$j q
 . s lineNo=$o(^zmwire("monitor","output",pid,""),-1)+1
 . s ^zmwire("monitor","output",pid,lineNo)=input
 . ;zsy "mupip intrpt "_pid_" >/dev/null"
 . zsy "kill -USR1 "_pid
 ;
 QUIT
 ;
monitoroutput
 ;
 n lineNo
 ;
 s lineNo=""
 f  s lineNo=$o(^zmwire("monitor","output",$j,lineNo)) q:lineNo=""  d
 . w ^zmwire("monitor","output",$j,lineNo)
 . k ^zmwire("monitor","output",$j,lineNo)
 QUIT
 ;
info
 ;
 n count,ignore,pid,response
 ;
 s response="m_wire_version:"_$p($t(mwireVersion+1),";;",2,2000)_$c(13,10)
 s pid="",count=0
 f  s pid=$o(^zmwire("connected",pid)) q:pid=""  d
 . s ignore=1
 . i pid=$j d
 . . s ignore=0
 . e  d
 . . l +^zmwire("connected",pid):0 e  s ignore=0
 . i ignore d
 . . l -^zmwire("connected",pid)
 . . k ^zmwire("connected",pid)
 . . k ^zmwire("monitor","listener",pid)
 . e  d
 . . s count=count+1
 s response=response_"connected_clients:"_count ;_$c(13,10)
 w "$"_$l(response)_$c(13,10)_response_$c(13,10)
 QUIT
 ;
auth(input)
 ;
 n pass
 s pass=$$MD5(input)
 i $d(^zmwire("auth",pass)) d
 . s authNeeded=0
 . s role=^zmwire("auth",pass)
 . w "+OK"_$c(13,10)
 e  d
 . w "-Invalid password"_$c(13,10)
 QUIT
 ;
setpassword(input)
 ;
 ; SETPASSWORD secret
 ; +OK <set as role=user>
 ;
 ; SETPASSWORD secret admin
 ; +OK
 ;
 n pass,newrole
 ;
 i $d(^zmwire("auth")),role'="admin" w "-Invalid command"_$c(13,10) QUIT
 i $$stripSpaces(input)="" w "-Invalid command"_$c(13,10) QUIT
 s newrole="user"
 i input[" " d
 . s newrole=$p(input," ",2)
 . s input=$p(input," ",1)
 i '$d(^zmwire("auth")) s newrole="admin"
 i newrole'="user",newrole'="admin" w "-Invalid role"_$c(13,10) QUIT
 ;
 s pass=$$MD5(input)
 s ^zmwire("auth",pass)=newrole
 w "+OK"_$c(13,10)
 QUIT
 ;
getGloRef(input)
 ;
 n gloName,gloRef,nb,subs
 ;
 s gloRef=input
 i $e(gloRef,1)'="^" s gloRef="^"_gloRef
 i $e(gloRef,$l(gloRef))="]" s gloRef=$e(gloRef,1,$l(gloRef)-1)
 s gloName=$p(gloRef,"[",1)
 s nb=$l(gloRef,"[")+2
 s subs=$p(gloRef,"[",2,nb)
 i subs="" QUIT gloName
 QUIT gloName_"("_subs_")"
 ;
set(input)
 ;
 n data,gloName,gloRef,inputr,len,nb,nsp,ok,subs,x
 ;
 ; SET myglobal["1","xx yy",3] 5
 ; hello
 ; +OK
 ; SET myGlo 5
 ; hello
 ; +OK
 ;
 i input[$c(13,10) d
 . s data=$p(input,$c(13,10),2,1000)
 . s input=$p(input,$c(13,10),1)
 s inputr=$re(input)
 s len=$re($p(inputr," ",1))
 i len'=0,+len=0 w "-Data length was not specified"_$c(13,10) QUIT
 s nsp=$l(input," ")+2
 s gloRef=$p(inputr," ",2,nsp)
 s gloRef=$re(gloRef)
 i $e(gloRef,1)'="^" s gloRef="^"_gloRef
 i $e(gloRef,$l(gloRef))="]" s gloRef=$e(gloRef,1,$l(gloRef)-1)
 s gloName=$p(gloRef,"[",1)
 s nb=$l(gloRef,"[")+2
 s subs=$p(gloRef,"[",2,nb)
 s gloRef=gloName
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 i subs'="" s gloRef=gloRef_"("_subs_")"
 i '$d(data)  d
 . s data=$$readChars(len)
 . ;r data#len
 . r ok 
 d log(data)
 i data["""" s data=$$replaceAll(data,"""","""""")
 s x="s "_gloRef_"="""_data_""""
 s $zt=$$zt()
 x x
 s $zt=""
 s response="+ok"_$c(13,10)
 w response
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("set: "_response_" sent")
 QUIT
 ;
getGlobalList()
 ;
 n arrString,comma,count,crlf,glo,gloRef,list,response,x
 ;
 s crlf=$c(13,10)
 i $zv["GT.M" d
 . s x="^%"
 . i $d(@x) s list(x)=""
 . f  s x=$order(@x) q:x=""  s list(x)=""
 . ;
 e  d
 . d getGlobalList^MDBMCache(.list)
 ;
 ;s count=0,glo=""
 ;f  s glo=$o(list(glo)) q:glo=""  s count=count+1
 ;s response="*"_count_crlf
 ;w response
 ;i $g(^zewd("trace"))=1 d trace^%zewdAPI("getGlobalList: "_response_" sent") 
 ;s glo=""
 ;f  s glo=$o(list(glo)) q:glo=""  d
 ;. s gloRef=$e(glo,2,$l(glo))
 ;. s response="$"_$l(gloRef)_crlf_gloRef_crlf
 ;. w response
 ;. i $g(^zewd("trace"))=1 d trace^%zewdAPI("getGlobalList: "_response_" sent") 
 s arrString="["
 s glo="",comma=""
 f  s glo=$o(list(glo)) q:glo=""  d
 . s gloRef=$e(glo,2,$l(glo))
 . s arrString=arrString_comma_""""_gloRef_""""
 . s comma=","
 s arrString=arrString_"]"
 s response="$"_$l(arrString)_crlf_arrString_crlf
 w response
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("getGlobalList: "_response_" sent")  ;
 QUIT
 ;
getGlobals()
 ;
 n arrString,comma,count,crlf,glo,gloRef,list,response,x
 ;
 s crlf=$c(13,10)
 i $zv["GT.M" d
 . s x="^%"
 . i $d(@x) s list(x)=""
 . f  s x=$order(@x) q:x=""  s list(x)=""
 . ;
 e  d
 . d getGlobalList^MDBMCache(.list)
 ;
 s count=0,glo=""
 f  s glo=$o(list(glo)) q:glo=""  s count=count+1
 s response="*"_count_crlf
 w response
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("getGlobalList: "_response_" sent") 
 s glo=""
 f  s glo=$o(list(glo)) q:glo=""  d
 . s gloRef=$e(glo,2,$l(glo))
 . s response="$"_$l(gloRef)_crlf_gloRef_crlf
 . w response
 . i $g(^zewd("trace"))=1 d trace^%zewdAPI("getGlobalList: "_response_" sent") 
 QUIT
 ;
get(input)
 ;
 n data,exists,gloRef,response,x
 ;
 ; GET myglobal["1","xx yy",3]
 ; $6
 ; foobar
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="s exists=$d("_gloRef_")"
 s $zt=$$zt()
 x x
 s $zt=""
 i exists'=1,exists'=11 w "$-1"_$c(13,10) QUIT
 s x="s data="_gloRef
 s $zt=$$zt()
 x x
 s $zt=""
 ;
 s response="$"_$l(data)_$c(13,10)_data_$c(13,10)
 ;
 w response
 QUIT
 ;
getGlobal(input)
 ;
 n data,exists,gloRef,json,response,x
 ;
 ; GET myglobal["1","xx yy",3]
 ; $6
 ; foobar
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="s exists=$d("_gloRef_")"
 s $zt=$$zt()
 x x
 s x="s data=$g("_gloRef_")"
 x x
 s $zt=""
 s json="{""value"":"""_data_""",""dataStatus"":"_exists_"}"
 ;
 s response="$"_$l(json)_$c(13,10)_json_$c(13,10)
 ;
 w response
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("getGlobal: response="_response)
 QUIT
 ;
kill(input)
 ;
 n i,glo,gloRef,nsp,p1,p2,x
 ;
 ; KILL myglobal["1","xx yy",3]
 ; +OK
 ;
 s nsp=$l(input," ")
 f i=1:1:nsp d
 . s glo=$p(input," ",i)
 . q:glo=""
 . s p1=$p(glo,"[",1)
 . s p2=$p(glo,"[",2,2000)
 . s p2=$e(p2,1,$l(p2)-1)
 . s glo=p1_"("_p2_")"
 . i glo'["zmwire" s glo(glo)=""
 s glo=""
 f  s glo=$o(glo(glo)) q:glo=""  d
 . s x="k ^"_glo
 . s $zt=$$zt()
 . x x
 . s $zt=""
 s response="+ok"_$c(13,10)
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("kill: response="_response)
 w response
 QUIT
 ;
data(input)
 ;
 n data,gloRef,x
 ;
 ; DATA myglobal["1","xx yy",3]
 ; :10
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="s data=$d("_gloRef_")"
 s $zt=$$zt()
 x x
 s $zt=""
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("input="_input_"; data="_data)
 w ":"_data_$c(13,10)
 QUIT
 ;
setJSON(input)
 ;
 n arr,crlf,del,error,flrc,inputr,gloRef,inputr,json,nb,nsp,props,ref,response,subs
 ;
 ; SETJSONSTRING myglobal["1","xx yy",3] CRLF {"a":123} CRLF 1
 ; +ok
 ;
 s crlf=$c(13,10),flrc=$c(10,13)
 s gloRef=$p(input,crlf,1)
 s input=$p(input,crlf,2,10000)
 s inputr=$re(input)
 s del=$p(inputr,flrc,1),del=$re(del)
 s inputr=$p(inputr,flrc,2,10000) ; in case it contains crlfs
 s json=$re(inputr)
 ;
 i $e(gloRef,1)'="^" s gloRef="^"_gloRef
 i $e(gloRef,$l(gloRef))="]" s gloRef=$e(gloRef,1,$l(gloRef)-1)
 s gloName=$p(gloRef,"[",1)
 s nb=$l(gloRef,"[")+2
 s subs=$p(gloRef,"[",2,nb)
 s gloRef=gloName
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 i subs'="" s gloRef=gloRef_"("_subs_")"
 s error=$$parseJSON^%zewdJSON(json,.props,1)
 i error'="" w "-Invalid JSON in setJSON: "_json_$c(13,10) QUIT
 ;
 i del d
 . s ref="k "_gloRef
 . x ref
 ;
 s ref="m "_gloRef_"=props"
 x ref
 s response="+ok"_$c(13,10)
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("setJSON: response="_response)
 ;
 w response
 ;
 QUIT
 ;
getJSON(input)
 ;
 n arr,inputr,gloRef,json,nb,nsp,ref,response,subs
 ;
 ; GETJSONSTRING myglobal["1","xx yy",3]
 ; $5
 ; {x:1}
 ;
 s gloRef=input
 i $e(gloRef,1)'="^" s gloRef="^"_gloRef
 i $e(gloRef,$l(gloRef))="]" s gloRef=$e(gloRef,1,$l(gloRef)-1)
 s gloName=$p(gloRef,"[",1)
 s nb=$l(gloRef,"[")+2
 s subs=$p(gloRef,"[",2,nb)
 s gloRef=gloName
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 i subs'="" s gloRef=gloRef_"("_subs_")"
 s ref="m arr="_gloRef
 x ref
 s json=$$arrayToJSON^%zewdJSON("arr")
 s response="$"_$l(json)_$c(13,10)_json_$c(13,10)
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("getJSON: response="_response)
 ;
 w response
 ;
 QUIT
 ;
incr(input)
 ;
 n data,gloRef,x
 ;
 ; INCR myglobal["1","xx yy",3]
 ; :4
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="s data=$increment("_gloRef_")"
 s $zt=$$zt()
 x x
 s $zt=""
 w ":"_data_$c(13,10)
 QUIT
 ;
incrby(input)
 ;
 n by,data,gloName,gloRef,inputr,len,nb,nsp,ok,subs,x
 ;
 ; INCRBY myglobal["1","xx yy",3] 3
 ; :7
 ;
 s inputr=$re(input)
 s by=$re($p(inputr," ",1))
 s nsp=$l(input," ")+2
 s gloRef=$p(inputr," ",2,nsp)
 s gloRef=$re(gloRef)
 i $e(gloRef,1)'="^" s gloRef="^"_gloRef
 i $e(gloRef,$l(gloRef))="]" s gloRef=$e(gloRef,1,$l(gloRef)-1)
 s gloName=$p(gloRef,"[",1)
 s nb=$l(gloRef,"[")+2
 s subs=$p(gloRef,"[",2,nb)
 s gloRef=gloName
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 i subs'="" s gloRef=gloRef_"("_subs_")"
 s x="s data=$increment("_gloRef_","_by_")"
 s $zt=$$zt() x x
 s $zt=""
 w ":"_data_$c(13,10)
 QUIT
 ;
function(input)
 ;
 n data,func,x
 ;
 ; FUNCTION label^rou("1","xx yy")
 ; $5
 ; hello
 ;
 s func=input
 i func["^",$e(func,1,2)'="$$" s func="$$"_func
 i func["class(",$e(func,1,2)'="##" s func="##"_func
 s x="s data="_func
 s $zt=$$zt()
 x x
 s $zt=""
 w "$"_$l(data)_$c(13,10)_data_$c(13,10)
 QUIT
 ;
decr(input)
 ;
 n data,gloRef,x
 ;
 ; DECR myglobal["1","xx yy",3]
 ; :3
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="s data=$increment("_gloRef_",-1)"
 s $zt=$$zt() x x
 s $zt=""
 w ":"_data_$c(13,10)
 QUIT
 ;
decrby(input)
 ;
 n by,data,gloName,gloRef,inputr,nb,nsp,ok,subs,x
 ;
 ; DECRBY myglobal["1","xx yy",3] 3
 ; :4
 ;
 s inputr=$re(input)
 s by=$re($p(inputr," ",1))
 s nsp=$l(input," ")+2
 s gloRef=$p(inputr," ",2,nsp)
 s gloRef=$re(gloRef)
 i $e(gloRef,1)'="^" s gloRef="^"_gloRef
 i $e(gloRef,$l(gloRef))="]" s gloRef=$e(gloRef,1,$l(gloRef)-1)
 s gloName=$p(gloRef,"[",1)
 s nb=$l(gloRef,"[")+2
 s subs=$p(gloRef,"[",2,nb)
 s gloRef=gloName
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 i subs'="" s gloRef=gloRef_"("_subs_")"
 s x="s data=$increment("_gloRef_",-"_by_")"
 s $zt=$$zt()
 x x
 s $zt=""
 w ":"_data_$c(13,10)
 QUIT
 ;
nextSubscript(input,direction)
 ;
 n crlf,data,gloRef,response,subscript,x,value
 ;
 ; NEXTSUBSCRIPT myglobal["1","xx yy",""]
 ; +abc
 ;
 s crlf=$c(13,10)
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="s subscript=$o("_gloRef_",direction)"
 s $zt=$$zt()
 x x
 s $zt=""
 s value=$g(^(subscript))
 s data=$d(^(subscript))
 ;
 s response="{""subscriptValue"":"""_subscript_""","
 s response=response_"""dataStatus"":"_data_","
 s response=response_"""dataValue"":"""_value_"""}"
 ;
 s response="$"_$l(response)_crlf_response_crlf
 i $g(^zewd("trace"))=1 d trace^%zewdAPI("nextsubscript: response="_response)
 w response
 ;
 QUIT
 ;
order(input)
 ;
 n data,gloRef,x
 ;
 ; ORDER myglobal["1","xx yy",""]
 ; +abc
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="s data=$o("_gloRef_")"
 s $zt=$$zt()
 x x
 s $zt=""
 i data="" w "$-1"_$c(13,10) QUIT
 w "$"_$l(data)_$c(13,10),data_$c(13,10)
 QUIT
 ;
reverseorder(input)
 ;
 n data,gloRef,x
 ;
 ; REVORDER myglobal["1","xx yy",""]
 ; +abc
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="s data=$o("_gloRef_",-1)"
 s $zt=$$zt()
 x x
 s $zt=""
 i data="" w "$-1"_$c(13,10) QUIT
 w "$"_$l(data)_$c(13,10),data_$c(13,10)
 QUIT
 ;
query(input)
 ;
 n data,gloRef,nb,p1,p2,x
 ;
 ; QUERY myglobal["1","xx yy"]
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="s data=$q("_gloRef_")"
 s $zt=$$zt()
 x x
 i data="" w "$-1"_$c(13,10) QUIT
 s data=$e(data,2,$l(data))
 s p1=$p(data,"(",1)
 s nb=$p(data,"(")+2
 s p2=$p(data,"(",2,nb)
 s p2=$e(p2,1,$l(p2)-1)
 s data=p1_"["_p2_"]"
 w "$"_$l(data)_$c(13,10),data_$c(13,10)
 s $zt=""
 QUIT
 ;
queryget(input)
 ;
 n data,gloRef,nb,odata,p1,p2,value,x
 ;
 ; QUERYGET myglobal["1","xx yy"]
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="s data=$q("_gloRef_")"
 s $zt=$$zt()
 x x
 i data="" w "$-1"_$c(13,10) QUIT
 s odata=data
 w "*2"_$c(13,10)
 s data=$e(data,2,$l(data))
 s p1=$p(data,"(",1)
 s nb=$p(data,"(")+2
 s p2=$p(data,"(",2,nb)
 s p2=$e(p2,1,$l(p2)-1)
 s data=p1_"["_p2_"]"
 w "$"_$l(data)_$c(13,10),data_$c(13,10)
 s value=@odata
 w "$"_$l(value)_$c(13,10)_value_$c(13,10)
 s $zt=""
 QUIT
 ;
lock(input)
 ;
 n gloName,gloRef,inputr,nb,nsp,ok,subs,time,x
 ;
 ; LOCK myglobal["1","xxyy"] 5
 ; +OK
 ;
 s inputr=$re(input)
 s time=$re($p(inputr," ",1))
 i time?1N.N d
 . s nsp=$l(input," ")+2
 . s gloRef=$p(inputr," ",2,nsp)
 e  d
 . s time=5
 . s gloRef=inputr
 s gloRef=$re(gloRef)
 i $e(gloRef,1)'="^" s gloRef="^"_gloRef
 i $e(gloRef,$l(gloRef))="]" s gloRef=$e(gloRef,1,$l(gloRef)-1)
 s gloName=$p(gloRef,"[",1)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s nb=$l(gloRef,"[")+2
 s subs=$p(gloRef,"[",2,nb)
 s gloRef=gloName
 i subs'="" s gloRef=gloRef_"("_subs_")"
 s x="s ok=1 l +"_gloRef_":"_time_" e  s ok=0"
 s $zt=$$zt()
 x x
 s $zt=""
 w ":"_ok_$c(13,10)
 QUIT
 ;
unlock(input)
 ;
 n gloRef,x
 ;
 ; UNLOCK myglobal["1","xxyy"]
 ; +OK
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 s x="l -"_gloRef
 s $zt=$$zt()
 x x
 s $zt=""
 w "+OK"_$c(13,10)
 QUIT
 ;
getAllSubscripts(input)
 ;
 n comma,data,exists,i,gloRef,len,rec,ref,subscripts,subs,subs1,x
 ;
 ; GETSUBSCRIPTS myglobal["1","xx yy"] 
 ; 
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 i $e(gloRef,$l(gloRef))=")" d
 . s x="s exists=$d("_gloRef_")"
 . s gloRef=$e(gloRef,1,$l(gloRef)-1)_","
 e  d
 . s x="s exists=$d("_gloRef_")"
 . s gloRef=gloRef_"("
 s $zt=$$zt()
 x x
 s $zt=""
 i 'exists w "$-1"_$c(13,10) QUIT
 ;
 s subs=""
 s subs1=subs i subs1["""" s subs1=$$replaceAll(subs1,"""","""""")
 s x="s subs=$o("_gloRef_""""_subs1_"""))"
 x x
 s len=3+$l(subs)
 s comma=","
 i subs'="" d
 . f  s subs=$o(^(subs)) q:subs=""  d
 . . s len=len+$l(comma)+2+$l(subs)
 s len=len+1
 s response="$"_len_$c(13,10)
 w response
 ;
 s x="s subs=$o("_gloRef_""""_subs1_"""))"
 x x
 s response="["""_subs_""""
 w response
 i subs'="" d
 . f  s subs=$o(^(subs)) q:subs=""  d
 . . s response=comma_""""_subs_""""
 . . w response
 s response="]"_$c(13,10)
 w response
 ;
 QUIT
 ;
orderall(input)
 ;
 n data,exists,i,gloRef,rec,subs,subs1,x
 ;
 ; ORDERALL myglobal["1","xx yy"] 
 ; *6
 ; $2
 ; aa
 ; $5
 ; hello
 ; $2
 ; bb
 ; $5
 ; world
 ; $3
 ; bba
 ; $-1
 ; 
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 i $e(gloRef,$l(gloRef))=")" d
 . s x="s exists=$d("_gloRef_")"
 . s gloRef=$e(gloRef,1,$l(gloRef)-1)_","
 e  d
 . s x="s exists=$d("_gloRef_")"
 . s gloRef=gloRef_"("
 s $zt=$$zt()
 x x
 i 'exists w "$-1"_$c(13,10) QUIT
 ;
 s subs="",rec=0
 k ^CacheTempEWD($j)
 f  d  q:subs=""
 . s subs1=subs i subs1["""" s subs1=$$replaceAll(subs1,"""","""""")
 . s x="s subs=$o("_gloRef_""""_subs1_"""))"
 . x x
 . i subs="" q
 . s rec=rec+1
 . s ^CacheTempEWD($j,rec)="$"_$l(subs)_$c(13,10)_subs_$c(13,10)
 . s x="s exists=$d("_gloRef_""""_subs_"""))"
 . x x
 . i exists=1!(exists=11) d
 . . s x="s data="_gloRef_""""_subs_""")"
 . . x x
 . . s rec=rec+1
 . . s ^CacheTempEWD($j,rec)="$"_$l(data)_$c(13,10)_data_$c(13,10)
 . e  d
 . . s rec=rec+1
 . . s ^CacheTempEWD($j,rec)="$-1"_$c(13,10)
 s $zt=""
 w "*"_rec_$c(13,10)
 f i=1:1:rec w ^CacheTempEWD($j,i)
 k ^CacheTempEWD($j)
 QUIT
 ;
mergefrom(input)
 ;
 n data,gloRef,i,params,resp,start,x
 ;
 ; MERGEFROM myglobal["1","a"]
 ; *6
 ; $1
 ; 1  <keys>
 ; $5
 ; hello  <data>
 ; $9
 ; 1,"a\"aa"  <keys> note escaping
 ; $5
 ; world
 ; $8
 ; 2,"cccc"
 ; $3
 ; foo
 ;
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 k ^CacheTempEWD($j)
 s x="m ^CacheTempEWD($j)="_gloRef
 s $zt=$$zt()
 x x
 s $zt=""
 s x=$q(^CacheTempEWD($j,""))
 i x="" w "*-1"_$c(13,10) k ^CacheTempEWD($j) QUIT
 f i=1:1 s x=$q(@x) q:x=""
 i $d(^CacheTempEWD($j))=1!($d(^CacheTempEWD($j))=11) s i=i+1
 w "*"_(i*2)_$c(13,10)
 i $d(^CacheTempEWD($j))=1!($d(^CacheTempEWD($j))=11) d
 . w "$-1"_$c(13,10)
 . w "$"_$l(^CacheTempEWD($j))_$c(13,10)_^CacheTempEWD($j)_$c(13,10)
 s x=$q(^CacheTempEWD($j,""))
 s start="^CacheTempEWD("_$j_","
 s params=$p(x,start,2,2000)
 s params=$e(params,1,$l(params)-1)
 s resp=params
 w "$"_$l(resp)_$c(13,10)_resp_$c(13,10)
 s data=@x
 w "$"_$l(data)_$c(13,10)_data_$c(13,10)  
 f i=1:1 s x=$q(@x) q:x=""  d
 . s params=$p(x,start,2,2000)
 . s params=$e(params,1,$l(params)-1)
 . s resp=params
 . w "$"_$l(resp)_$c(13,10)_resp_$c(13,10)  
 . s data=@x
 . i data="" d
 . . w "$-1"_$c(13,10)
 . e  d
 . . w "$"_$l(data)_$c(13,10)_data_$c(13,10)  
 k ^CacheTempEWD($j)
 QUIT
 ;
mergeto(input)
 ;
 n data,dataLength,error,gloRef,i,key,keyLength,noOfRecs,x
 ;
 ; MERGETO myglobal["1","a"]
 ; *6
 ; $1
 ; 1   <keys>
 ; $5
 ; hello  <data>
 ; $7
 ; 1,"aaa"  <keys>
 ; $5
 ; world   <data>
 ; $8
 ; 2,"cccc"
 ; $3
 ; foo
 ; +OK
 ;   note $-1 for key length means no key - data put at top level
 ;
 s $zt=$$zt()
 s gloRef=$$getGloRef(input)
 i gloRef["^zmwire" w "-No access allowed to this global"_$c(13,10) QUIT
 i $e(gloRef,$l(gloRef))=")" s gloRef=$e(gloRef,1,$l(gloRef)-1)
 ;
 r noOfRecs
 i $e(noOfRecs,1)'="*" w "-Invalid: expected number of records"_$c(13,10) QUIT
 s noOfRecs=+$e(noOfRecs,2,$l(noOfRecs))
 i noOfRecs'?1N.N w "-Invalid format for number of records"_$c(13,10) QUIT
 i noOfRecs=0 QUIT "+OK"_$c(13,10) QUIT
 i (noOfRecs#2)=1 w "-Invalid: no of records must be an even number"_$c(13,10) QUIT
 s noOfRecs=noOfRecs/2
 k ^CacheTempEWD($j)
 s error=""
 k ^CacheTempEWD($j)
 f i=1:1:noOfRecs d  q:error'=""
 . r keyLength
 . i $e(keyLength,1)'="$" s error="Invalid record "_i_": record length" q
 . s keyLength=+$e(keyLength,2,$l(keyLength))
 . i keyLength=-1 d
 . . s key=""
 . e  d
 . . i keyLength'?1N.N s error="Invalid record "_i_": bad format for record length" q
 . . i keyLength=0 s error="Invalid record "_i_": record length cannot be zero" q
 . . s key=$$readChars(keyLength)
 . . r ok d log(key)
 . . ;r key#keyLength,ok d log(key)
 . . i key["\""" s key=$$replaceAll(key,"\""","""""")
 . i error'="" q
 . r dataLength
 . i $e(dataLength,1)'="$" s error="Invalid record "_i_": expected data length" q
 . s dataLength=+$e(dataLength,2,$l(dataLength))
 . i dataLength'=-1,dataLength'?1N.N s error="Invalid record "_i_": bad format for data length" q
 . i dataLength=-1 d
 . . s data=""
 . e  d
 . . s data=$$readChars(dataLength)
 . . r ok
 . . d log(data)
 . . ;r data#dataLength,ok d log(data)
 . . i data["""" s data=$$replaceAll(data,"""","""""")
 . i key="" d
 . . n gloRef1
 . . s gloRef1=gloRef
 . . i gloRef["(" s gloRef1=gloRef1_")"
 . . s x="s "_gloRef1_"="""_data_""""
 . e  d
 . . n gloRef1
 . . s gloRef1=gloRef
 . . i gloRef'["(" d
 . . . s gloRef1=gloRef1_"("
 . . e  d
 . . . s gloRef1=gloRef1_","
 . . s x="s "_gloRef1_key_")="""_data_""""
 . s ^CacheTempEWD($j,i)=x
 i error'="" w "-"_error_$c(13,10) QUIT
 f i=1:1:noOfRecs d
 . s x=^CacheTempEWD($j,i)
 . x x
 k ^CacheTempEWD($j)
 w "+OK"_$c(13,10)
 s $zt=""
 QUIT
 ;
mdate
 ;
 n date,day,time
 ;
 s date=$h
 s day=+date
 w "*2"_$c(13,10)_"$"_$l(day)_$c(13,10)_day_$c(13,10)
 s time=$p(date,",",2)
 w "$"_$l(time)_$c(13,10)_time_$c(13,10)
 QUIT
 ;
tstart
 s $zt=$$zt()
 TSTART
 w "+OK"_$c(13,10)
 s $zt=""
 QUIT
 ;
tcommit
 s $zt=$$zt()
 TCOMMIT
 w "+OK"_$c(13,10)
 s $zt=""
 QUIT
 ;
trollback
 s $zt=$$zt()
 TROLLBACK
 w "+OK"_$c(13,10)
 s $zt=""
 QUIT
 ;
zv
 w "+"_$zv_$c(13,10)
 QUIT
 ;
zt()
 i $zv["GT.M" QUIT "g executeError^zmwire"
 QUIT "executeError^zmwire"
 ;
processid
 w ":"_$j_$c(13,10)
 QUIT
 ;
ping
 n response
 s response="+PONG"_$c(13,10)
 i $g(^zewd("trace"))=1 d trace^%zewdAPI(response_" about to be written")
 w response
 i $g(^zewd("trace"))=1 d trace^%zewdAPI(response_" written")
 QUIT
 ;
executeError
 w "-Invalid Command"_$c(13,10)
 g loop
 ;
replaceAll(InText,FromStr,ToStr) ; Replace all occurrences of a substring
 ;
 n p
 ;
 s p=InText
 i ToStr[FromStr d  QUIT p
 . n i,stop,tempText,tempTo
 . s stop=0
 . f i=0:1:255 d  q:stop
 . . q:InText[$c(i)
 . . q:FromStr[$c(i)
 . . q:ToStr[$c(i)
 . . s stop=1
 . s tempTo=$c(i)
 . s tempText=$$replaceAll(InText,FromStr,tempTo)
 . s p=$$replaceAll(tempText,tempTo,ToStr)
 f  q:p'[FromStr  S p=$$replace(p,FromStr,ToStr)
 QUIT p
 ;
replace(InText,FromStr,ToStr) ; replace old with new in string
 ;
 n np,p1,p2
 ;
 i InText'[FromStr q InText
 s np=$l(InText,FromStr)+1
 s p1=$p(InText,FromStr,1),p2=$p(InText,FromStr,2,np)
 QUIT p1_ToStr_p2
 ;
readChars(length)
 ;
 n data,i,x
 ;
 s data=""
 f i=1:1:length r *x s data=data_$c(x)
 QUIT data
 ;
stripSpaces(string)
 i $zv["Cache" QUIT $$stripSpaces^MDBMCache(string)
 ;
 s string=$$stripLeadingSpaces(string)
 QUIT $$stripTrailingSpaces(string)
 ;
stripLeadingSpaces(string)
 ;
 n i
 ;
 f i=1:1:$l(string) QUIT:$e(string,i)'=" "
 QUIT $e(string,i,$l(string))
 ;
stripTrailingSpaces(string)
 ;
 n i,spaces,new
 ;
 s spaces=$$makeString(" ",100)
 s new=string_spaces
 QUIT $p(new,spaces,1)
 ;
makeString(char,len) ; create a string of len characters
 ;
 n str
 ;
 s str="",$p(str,char,len+1)=""
 QUIT str
 ;
MD5(string)
 ;
 ; n hash
 ;
 i $zv["Cache" QUIT $$MD5^MDBMCache(string)
 ;
 QUIT $$MD5^%ZMGWSIS(string,1,1)
 ;

zmwireDaemon^INT^1^61781,53875^0^1
zmwireDaemon ; M/Wire Protocol Daemon process for Cache
 ;
 ; ----------------------------------------------------------------------------
 ; | M/Wire                                                                   |
 ; | Copyright (c) 2004-9 M/Gateway Developments Ltd,                         |
 ; | Reigate, Surrey UK.                                                      |
 ; | All rights reserved.                                                     |
 ; |                                                                          |
 ; | http://www.mgateway.com                                                  |
 ; | Email: rtweed@mgateway.com                                               |
 ; |                                                                          |
 ; | This program is free software: you can redistribute it and/or modify     |
 ; | it under the terms of the GNU Affero General Public License as           |
 ; | published by the Free Software Foundation, either version 3 of the       |
 ; | License, or (at your option) any later version.                          |
 ; |                                                                          |
 ; | This program is distributed in the hope that it will be useful,          |
 ; | but WITHOUT ANY WARRANTY; without even the implied warranty of           |
 ; | MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            |
 ; | GNU Affero General Public License for more details.                      |
 ; |                                                                          |
 ; | You should have received a copy of the GNU Affero General Public License |
 ; | along with this program.  If not, see <http://www.gnu.org/licenses/>.    |
 ; ----------------------------------------------------------------------------
 ;
 QUIT
 ;
 ;  M/Wire Daemon for Cache
 ;
 ;  Build 6 Beta
 ;
 ;  Start using:
 ;
 ;    job start^zmwireDaemon
 ;
 ;  You should now be able to connect M/Wire clients to port 6330
 ;
 ;  Use ^RESJOB to halt the daemon if required
 ;
 ;  Change the line below to modify the port number used by M/Wire
 ;
port
 ;;6330
 ;
start
 i $zv["GT.M" QUIT
 n io,port,x
 ;
 l +^zmwire("daemon"):0 e  QUIT  ; already running
 ;
 s io="|TCP|1" 
 s port=$p($t(port+1),";;",2) 
 o io:(:port:"PTA"):20 e  QUIT
 u io
loop
 r x
 j child:(:5:io:io)
 g loop
 ;
child
 ;
 u $io:(::"+Q":$c(10))
 d command^zmwire
 QUIT
 ;



